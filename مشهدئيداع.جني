ئورد مكون، عنصرمخصص، عنصرمربعنص؛
ئورد المستند من مستند؛
ئورد طافية من مكون.طافية؛
ئورد غمازة من مكون.غمازة؛
ئورد نعملا من مكون.نعملا؛
ئورد زر من مكون.زر؛
ئورد قابمحلي من قاعدب؛
ئورد الئان من تاريخ؛

ئورد مختطمشروع من صنفمجال.مشروع؛
ئورد الكل ك مجالجيتهب من صنفمجال.جيتهب؛
ئورد الكل ك مجالبطاقة من صنفمجال.بطاقة؛
ئورد الكل ك مجالمشروع من صنفمجال.مشروع؛

ئعلن مشهدئيداع ():
	يمدد مكون()؛
تم

ئعلن مشهدئيداع.عندنقرئلغي ():
	طافية طف = المستند.ئستعلم((<طافية>))؛
	طف.ئخفي()؛
تم

ئعلن مشهدئيداع.عندنقرئودع (هدف، حدت) ك وعد:
	زر زرئيسي = هدف؛
	غمازة غمز = المستند.ئستعلم((<غمازة>))؛
	عنصرمربعنص مربعن = هدا.ئستعلم((<مربعنص>))؛
	ئدا مربعن.قيمة == '':
		غمز.خدحالة({
			رسالة: 'يرجا ئدخال رسالة الئيداع'
		})؛
		ئرجع؛
	تم
	
	زرئيسي.خدحالة({معطل: صحيح})؛
	
	مختطمشروع مشروعحالي = ريتما مجالمشروع.مشروعحالي؛
	مركب[] {نصية مسار، نصية محتوا} تعديلات = ريتما مجالبطاقة.بطاقاتئلامحتوا(قابمحلي)؛
	
	حاول:
		ريتما مجالجيتهب.ئودعملفات(مشروعحالي.توكن، مشروعحالي.مالك، مشروعحالي.مستودع، مشروعحالي.فرعمفترض، تعديلات، مربعن.قيمة، مشروعحالي.ئسمشروع)؛	
		ريتما مجالمشروع.خدئاخرتعديل(قابمحلي، الئان())؛
		هدا.عندنقرئلغي()؛
	ئستدرك ف ك فشل:
		هدا.عندنقرئلغي()؛
		نعملا نعلا = المستند.ئستعلم((<نعملا>))؛
		نعلا.خدحالة({
			رسالة: 'تعدر الئيداع، تحقق من صلاحيات توكن الولوج'،
			ئيقو: 'خطر-دائرة'،
			زرنعم: عدم،
			زرلا: 'موافق'
		})؛
	تم
تم

ئعلن مشهدئيداع.صيير ():
	ئرجع (
		<قسم صنف="مخطط-طولي فجوة-4">
			<قسم صنف="مخطط-طولي فجوة-1">
				<حيز صنف="ئبيض نصصج" سمة="line-height:1.3rem;">رسالة الئيداع</حيز>
				<مربعنص صنف="ط-مئتان مقوس خلف-حيادي-700 نص-ئبيض نصصج" ئستنب="ئدخل رسالة الئيداع" سمة="line-height:1.3rem;"></مربعنص>
			</قسم>
			<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-نهاية ئرصف-نهاية">
				<زر تانوي عادي قيمة="ئلغي" نوع="عادي" عندنقر="عندنقرئلغي" صنف="نصصج"></زر>
				<زر رئيسي قيمة="ئودع" نبرة="ئحمر-600" عندنقر="عندنقرئودع" صنف="نصصج"></زر>
			</قسم>
		</قسم>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مشهدئيداع'، مشهدئيداع)؛
تم

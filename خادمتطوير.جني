ئورد decodeURI من '//'؛

ئورد ئساسية.عاملخدمة؛
ئورد ئساسية.جواب؛
ئورد ئساسية.ترويسة؛
ئورد ردمنئمتداد من ئساسية.نوعميم؛

ئورد مكون.بتشدبي؛

ئورد الكل ك مجالمشروع من صنفمجال.مشروع؛

ئعلن خادمتطوير ():
	يمدد عاملخدمة()؛
	هدا.ئستبدئ()؛
تم

ئعلن خادمتطوير.تببت ():
	قل 'تتبيت خادم'؛
تم

ئعلن ردجوابخام (نصية ئسملف، ترويسة[] ترجواب) ك وعد جواب:
	بتشدبي بتش؛
	ئدا ئسملف.يبدئب('ملفا/'):
		بتش = بتشدبي('jinni')؛
	وئلا:
		بتش = بتشدبي('jinnibin')؛
	تم
	
	مركب {نصية _معرف، نصية content} ملف = ريتما بتش.ئجلب(ئسملف)؛
	
	ئدا ليس ملف:
		قل ئسملف؛
		قل ملف؛
	وئلا:
		ئرجع جواب (
			محتوا: ملف['content'] ئو ملف['محتوا']،
			ترويسات: ترجواب
		)؛
	تم
تم

ئعلن خادمتطوير.ئجلب (ح):
	نصية رابط = decodeURI(ح.request.url)؛
	نصية[] مقسم = رابط.قسسم('/')؛
	نصية ئسملف = مقسم[مقسم.طول-1]؛
	ئدا رابط.يتضمن('/ئختبر'):
		ئدا ئسملف.ينتهيب('.mjs') ئو ئسملف.ينتهيب('.js'):
			#يقرئ محتوا الملف المطلوب من القاعدب
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'جافاسكربت')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		وئلا ئدا ئسملف.ينتهيب('.css'):
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'ورقسمة')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		وئلا ئدا ئسملف == 'تحميل':
			ح['respondWith'](جواب (
				محتوا: (
					<html lang="ar" dir="rtl" صنف="ط-كامل">
						<رئس>
							<ميطا charset="utf-8"></ميطا>
							<ميطا name="viewport" content="width=device-width, initial-scale=0.9, maximum-scale=0.9, user-scalable=0"></ميطا>
							<عنوان>تحميل</عنوان>
							<script src="/debug.js"></script>
							<سمة>.ط-كامل{height:100%;}.مخطط-طولي{display:flex;flex-direction:column;}.ئصفف-وسط{align-items:center}.ئرصف-وسط{justify-content:center}.ع-كامل{width:100%;}.خلف-رمادي-700{background-color:rgb\(55 65 81\)}.ئبيض{color:rgb\(255 255 255\)}.حش-0{padding:0px;}.هش-0{margin:0px;}.نصصج{font-size:0.75rem;line-height:1rem;}</سمة>
							<سمة>						
							@font-face { 
								font-family: 'كوكبمونو'; 
								src: url\('/ملفا/KawkabMonoTechRegular.ttf'\); format\('ttf'\);
							}
							body, body * {font-family: 'كوكبمونو';}
							</سمة>
						</رئس>
						<جسم صنف="ط-كامل تصفير فائض-مخفي خلف-رمادي-700 حش-0 هش-0">
							<قسم صنف="مخطط-طولي ئصفف-وسط ئرصف-وسط ط-كامل ع-كامل فائض-مخفي">
								<حيز صنف="ئبيض نصصج">تحميل</حيز>
							</قسم>
						</جسم>
					</html>
				)، 
				ترويسات: [ترويسة('يرجع'، 'ئشتمل')]
			))؛
		وئلا ئدا ليس ئسملف.يتضمن('.'):
			ح['respondWith'](جواب (
				محتوا: (
					<html lang="ar" dir="rtl" صنف="ط-كامل">
						<رئس>
							<ميطا charset="utf-8"></ميطا>
							<عنوان>%{ئسملف}</عنوان>
							<ميطا name="viewport" content="width=device-width, initial-scale=0.9, maximum-scale=0.9, user-scalable=0"></ميطا>
							<link rel="stylesheet" href="/ئختبر/style.css"></link>
							<script src="/debug.js"></script>
							<script type="module">
								import "/ئختبر/%{ئسملف}.mjs"
							</script>
						</رئس>
						<جسم صنف="ط-كامل تصفير فائض-مخفي">
						
						</جسم>
					</html>
				)، 
				ترويسات: [ترويسة('يرجع'، 'ئشتمل')]
			))؛
		وئلا ئدا رابط.يتضمن('ملفا/'):
			نصية[] مقسم = ئسملف.قسسم('.')؛
			نصية ئمتداد = مقسم[1]؛
			نصية نوعميم = ردمنئمتداد(ئمتداد)؛
			ئدا نوعميم == '':
				نوعميم = 'نصصرف'؛
			تم
			جواب جو = ردجوابخام("ملفا/%{ئسملف}"، [ترويسة('يرجع'، نوعميم)])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		وئلا:
			#يقرئ محتوا الملف المطلوب من القاعدب
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'نصصرف')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		تم
	وئلا:
		ئرجع؛
	تم
تم

ئعلن خادمتطوير.نششط ():
	قل 'تنشيط خادم'؛
تم

ئعلن مدخل ():
	خادمتطوير()؛
تم


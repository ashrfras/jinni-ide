ئورد decodeURI من '//'؛

ئورد ئساسية.عاملخدمة؛
ئورد ئساسية.جواب؛
ئورد ئساسية.ترويسة؛

ئورد مكون.بتشدبي؛

ئورد الكل ك مجالمشروع من صنفمجال.مشروع؛

ئعلن خادمتطوير ():
	يمدد عاملخدمة()؛
	هدا.ئستبدئ()؛
تم

ئعلن خادمتطوير.تببت ():
	قل 'تتبيت خادم'؛
تم

ئعلن ردجوابخام (نصية ئسملف، ترويسة[] ترجواب) ك وعد جواب:
	بتشدبي بتش = بتشدبي('jinnibin')؛
	مركب {نصية _معرف، نصية content} ملف = ريتما بتش.ئجلب(ئسملف)؛
	ئدا ليس ملف:
		قل ئسملف؛
		قل ملف؛
	وئلا:
		ئرجع جواب (
			محتوا: ملف['content']،
			ترويسات: ترجواب
		)؛
	تم
تم

ئعلن خادمتطوير.ئجلب (ح):
	نصية رابط = decodeURI(ح.request.url)؛
	نصية[] مقسم = رابط.قسسم('/')؛
	نصية ئسملف = مقسم[مقسم.طول-1]؛
	ئدا رابط.يتضمن('/ئختبر'):
		ئدا ئسملف.ينتهيب('.mjs') ئو ئسملف.ينتهيب('.js'):
			#يقرئ محتوا الملف المطلوب من القاعدب
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'جافاسكربت')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		وئلا ئدا ئسملف.ينتهيب('.css'):
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'ورقسمة')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		وئلا ئدا ليس ئسملف.يتضمن('.'):
			ح['respondWith'](جواب (
				محتوا: (
					<html lang="ar" dir="rtl" صنف="ط-كامل">
						<رئس>
							<ميطا charset="utf-8"></ميطا>
							<عنوان>%{ئسملف}</عنوان>
							<ميطا name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=0"></ميطا>
							<link rel="stylesheet" href="/ئختبر/style.css"></link>
							<script type="module">
								import "/ئختبر/%{ئسملف}.mjs"
							</script>
						</رئس>
						<جسم صنف="ط-كامل تصفير فائض-مخفي">
						
						</جسم>
					</html>
				)، 
				ترويسات: [ترويسة('يرجع'، 'ئشتمل')]
			))؛
		وئلا:
			#يقرئ محتوا الملف المطلوب من القاعدب
			جواب جو = ردجوابخام(ئسملف، [ترويسة('يرجع'، 'نصصرف')])؛
			ح['waitUntil'](جو)؛
			ح['respondWith'](جو)؛
		تم
	وئلا:
		ئرجع؛
	تم
تم

ئعلن خادمتطوير.نششط ():
	قل 'تنشيط خادم'؛
تم

ئعلن مدخل ():
	خادمتطوير()؛
تم


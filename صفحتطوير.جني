ئورد حدت، حدتمخصص؛
ئورد مكون، عنصرمخصص، عنصرئدخال، عنصر، عنصرئشتمل؛
ئورد المستند من مستند؛

ئورد ئيقو من مكون.ئيقو؛
ئورد طافية من مكون.طافية؛
ئورد نعملا من مكون.نعملا؛

ئورد مشهدوحدات، مشهدبطاقات، مشهدمرشحات، مشهدئيداع، مشهدنشر؛
ئورد الكل من قاعدب؛
ئورد نتجقاب من بتشدبي؛
ئورد مختطمشروع، مختطبطاقة من مختطة؛

ئعلن صفحتطوير ():
	يمدد مكون()؛
	
تم

ئعلن صفحتطوير.نقرعرضمنسدلة ():
	عنصرئشتمل منسدلة = هدا.ئستعلم('.منسدلة')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('مخفي'، 'مرئي')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('بلاعرض'، 'فلكس')؛
تم

ئعلن صفحتطوير.نقرئخفيمنسدلة ():
	عنصرئشتمل منسدلة = هدا.ئستعلم('.منسدلة')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('مرئي'، 'مخفي')؛
تم

ئعلن صفحتطوير.نقرئنشئبطاقة ():
	طافية طاف = المستند.ردعنصربمعرف('طافبطاقة')؛
	طاف.خدحالة({
		جسم: 'مشهدبطاقة'،
		بيا: عدم
	})؛
تم

ئعلن صفحتطوير.عندنقرئيداع ():
	#طافية طاف = هدا.ئستعلم((<طافية>))؛
	هدا.نقرئخفيمنسدلة()؛
	طافية طاف = المستند.ردعنصربمعرف('طفخيارين')؛
	طاف.خدحالة({
		جسم: 'مشهدئيداع'،
		عنوان: 'ئيداع التعديلين'
	})؛
تم

ئعلن صفحتطوير.ئعرضطفنشر ():
	طافية طاف = المستند.ردعنصربمعرف('طفخيارين')؛
	طاف.خدحالة({
		جسم: 'مشهدنشر'،
		عنوان: 'نشر المشروع'،
		تحتعنوان: 'تدكر تغيير الكلمتسر بعد النشر'
	})؛
تم

ئعلن صفحتطوير.عندنقرنشر ():
	هدا.نقرئخفيمنسدلة()؛
	#تحقق من وجود تعديلين غير مودوعين
	مختطمشروع مشروعي = قاعدب.مشروعحالي؛
	ئدا مشروعي.ئاخرتعديل > مشروعي.ئاخرئيداع:
		نعملا نعلا = هدا.ئستعلم((<نعملا>))؛
		نعلا.خدحالة({
			رسالة: 'توجد تعديلات لم يتم ئيداعها. هل ترغب في الئيداع؟'،
			عندنعم: (دالة(): هدا.عندنقرئيداع())،
			عندلا: (دالة(): هدا.ئعرضطفنشر())،
			ئيقو: 'تنبيه-دائرة'
		})؛
	وئلا:
		هدا.ئعرضطفنشر()؛
	تم
تم

ئعلن صفحتطوير.صيير ():
	ئرجع (
		<قسم ئسم="ترويس" صنف="ئرصف-وسط مخطط-طولي فجوة-4 ضلصغ خلف-ئسود حشس-4 حشص-2 ئطار-رمادي-700 لون-ئسود ئطارت1">
			<قسم صنف="مخطط-عرضي فجوة-4">
				<قسم عندنقر="نقرقائموحدة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة نص-رمادي-300 مقوسكب تئشير:ئطار-ئبيض">
					<ئيقو ئسم="قائمة" صنف="نصكجج"></ئيقو>
				</قسم>
				<رابط عنونت="/" صنف="نفسه-وسط">
					<صورة مصدر="/ملفا/شعار.svg" طول="30" صنف="ئصفف-ضمني-وسط"></صورة>
				</رابط>
				<ئدخال معرف="ئدخبحتبطاقات" نوع="نص" ستنب="ئكتب هنا للبحت" صنف="ئطاربلا خلف-ئسود نص-ئبيض يكبر حشيم-3 حشص-3"></ئدخال>
			
				<قسم صنف="مخطط فجوة-3">
					<قسم عندنقر="نقرئنشئبطاقة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة تئشير:خلف-رمادي-900 نص-رمادي-300 مقوسكب ئطار1 ئطار-رمادي-700 تئشير:ئطار-ئبيض حشس-3">
						<ئيقو ئسم="زائد" صنف="نصكجج"></ئيقو>
					</قسم>
					<قسم عندنقر="نقرعرضمنسدلة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة تئشير:خلف-رمادي-900 نص-رمادي-300 مقوسكب ئطار1 ئطار-رمادي-700 تئشير:ئطار-ئبيض حشس-3">
						<ئيقو ئسم="سهم-تصفح-تحت" صنف="نصكجج"></ئيقو>
					</قسم>
					<قسم صنف="مخطط-طولي فجوة-1 منسدلة موضعمطلق محورض40 خلف-رمادي-900 مقوسكج ئطار1 ئطار-رمادي-700 ضلكج نهاية-4 فوق-14 مخفي حش-3 مقوسكب ع-36">
						<قسم عندنقر="نقرئخفيمنسدلة" صنف="موضعتابت فوق-0 يمين-0 ستارمنسدل ط-شاشة ع-شاشة خلف-شفاف محورض30">
						</قسم>
						<قسم صنف="مخطط-عرضي يلتف فجوة-2 نص-ئبيض ئصفف-وسط ئرصف-وسط محورض50">
							<ئيقو ئسم="جيت-طلب-سحب" عندنقر="عندنقرئيداع" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض50"></ئيقو>
							<ئيقو ئسم="رفع-مربع" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض50"></ئيقو>
							<ئيقو ئسم="ئنترنت" عندنقر="عندنقرنشر" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض50"></ئيقو>
						</قسم>
					</قسم>
				</قسم>
			</قسم>
		</قسم>
		
		<قسم صنف="مخطط-عرضي">
			<قسم معرف="جهتبطاقات" صنف="خلف-حيادي-900 مخطط-طولي ع-كامل مت:ع-ئلاربع">
				<مشهدمرشحات></مشهدمرشحات>
				<مشهدبطاقات></مشهدبطاقات>
			</قسم>
			<قسم معرف="ترويمعاينة" صنف="مخطط ئصفف-وسط خلف-ئسود موضع-مطلق فوق-0 ط-8 ع-كامل بلاعرض مت:بلاعرض">
				<ئيقو عندنقر="ئخفمعاينة" ئسم="سهمقصيمين" صنف="نصكجج مئشر-سبابة نص-ئبيض"></ئيقو>
			</قسم>
			<قسم معرف="قسمتحجيم" صنف="فوق-0 ع-2 مئشر-تحجيم-عرضي خلف-ئحمر-700"></قسم>
			<ئفريم معرف="ئفريمعاينة" صنف="ئطار-رمادي-700 خلف-ئبيض ئطارب1 موضع-مطلق فوق-0 ط-كامل ع-كامل يمين-كامل مت:فوق-0 مت:موضع-نسبي مت:ع-ربع مت:يمين-تلقا مت:ط-تلقا"></ئفريم>
		</قسم>
		
		<مشهدوحدات></مشهدوحدات>
		
		<طافية معرف="طفخيارين" عنصر-بطاقة="خلف-حيادي-900" صنف="داكن" تابتة></طافية>
		<نعملا صنف="داكن" نبرة="ئحمر-700"></نعملا>
	)؛
تم

ئعلن صفحتطوير.عندنقرمرشح (حدتمخصص ح؟):
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوح = المستند.ئستعلم((<مشهدوحدات>))؛
	ئدا (ليس ح) ئو (ح.بيا == '1'): #الكل
		مشهوح.خدحالة({
			مساروحدتحالية: عدم
		})؛
		مشهبط.حممل()؛
	وئلا ئدا ح.بيا == '2': #مفتوحة مئخرا
		مشهبط.خدحالة({
			بطاقات: مشهبط.ئاخرمفتوح
		})؛
	تم
تم

ئعلن صفحتطوير.عندبحتبطاقات (حدت ح، عنصرئدخال عنصر) ك وعد:
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوحد = المستند.ئستعلم((<مشهدوحدات>))؛
	نصية مبحت = عنصر.قيمة؛
	نتجقاب نتج = ريتما قاعدب.قابمحلي.ئستعلم(
		حدد: {
			نوع: 'بطاقة'
		}
	)؛
	ئدا مشهوحد.حالة.مساروحدتحالية:
		مشهوحد.خدحالة({
			مساروحدتحالية: عدم
		})؛
	تم
	مختطبطاقة[] بطا = نتج.وتيقات؛
	ئدا مبحت لا= '':
		بطا = بطا.رششح(دالة(وتق): وتق.محتوا.يتضمن(مبحت))؛
	تم
	مشهبط.خدحالة({
		بطاقات: بطا
	})؛
تم

ئعلن صفحتطوير.حممل ():
	المستند.عنوان = قاعدب.مشروعحالي.ئسمشروع + ' - جني'؛
	عنصرئشتمل ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	مشهدمرشحات مشهمرشح = هدا.ئستعلم((<مشهدمرشحات>))؛
	عنصرئدخال ئدخال = المستند.ردعنصربمعرف('ئدخبحتبطاقات')؛
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوحد = المستند.ئستعلم((<مشهدوحدات>))؛
	
	مشهمرشح.ئضفمستمعحدت('نقرمرشح'، دالة (حدتمخصص ح): هدا.عندنقرمرشح(ح))؛
	ئدخال.ئضفمستمعحدت('تركمفتاح'، دالة (حدت ح): هدا.عندبحتبطاقات(ح، ئدخال))؛
	مشهبط.ئضفمستمعحدت('حدفبطاقة'، دالة (حدتمخصص ح): مشهوحد.حددت())؛
	مشهبط.ئضفمستمعحدت('حدفوحدة'، دالة (حدتمخصص ح): هدا.عندنقرمرشح())؛
	هدا.ئربطحدتين()؛
تم

ئعلن صفحتطوير.نقرقائموحدة ():
	مكون مشهدوحدات = المستند.ئستعلم((<مشهدوحدات>))؛
	عنصر عنص = مشهدوحدات.ئستعلم('.ط-كامل')؛
	ئدا عنص.ئسمصنف.يتضمن('مخفي'):
		عنص.ئسمصنف = عنص.ئسمصنف.عووض('مخفي'، 'مرئي')؛
	وئلا:
		عنص.ئسمصنف = عنص.ئسمصنف.عووض('مرئي'، 'مخفي')؛
	تم
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('صفحتطوير'، صفحتطوير)؛
تم

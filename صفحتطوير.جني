ئورد encodeURI من '//'؛
ئورد console من '//'؛
ئورد حدت، حدتمخصص؛
ئورد مكون، عنصرمخصص، عنصرئدخال، عنصر، عنصرئشتمل؛
ئورد المستند من مستند؛
ئورد النافدة من نافدة؛
ئورد المتصفح من متصفح؛
ئورد وضيفة؛
ئورد صحيحمننصية من عدد؛
ئورد ئيقو من مكون.ئيقو؛
ئورد طافية من مكون.طافية؛
ئورد نعملا من مكون.نعملا؛
ئورد غمازة من مكون.غمازة؛
ئورد ترجم من مكون.مترجني؛
ئورد ئستولدجيسس من مكون.جيسس؛
ئورد خادمتطوير؛
ئورد الكل ك مجالبطاقة من صنفمجال.بطاقة؛
ئورد الكل ك مجالوحدة من صنفمجال.وحدة؛
ئورد مختطبطاقة من صنفمجال.بطاقة؛
ئورد الكل ك مجالمشروع من صنفمجال.مشروع؛
ئورد مختطمشروع من صنفمجال.مشروع؛
ئورد مختطوحدة من صنفمجال.وحدة؛
ئورد مشهدوحدات، مشهدبطاقات، مشهدمرشحات، مشهدبطاقة، مشهدئيداع، مشهدمكون، مشهدمعاينة، مشهدنشر؛
ئورد الكل من قاعدب؛
ئورد نتجقاب من بتشدبي؛
ئورد الكل من سياق؛
ئورد ئساسية.نف.ملف؛

ئورد ئكشفتمرير من مكون.كاشفتمرير؛


ئعلن صفحتطوير ()؛
ئعلن صفحتطوير.نقرئنشئبطاقة ()؛
ئعلن صفحتطوير.نقرقائموحدة ()؛
ئعلن صفحتطوير.عندنقرنشر ()؛
ئعلن صفحتطوير.نقرعرضمنسدلة ()؛
ئعلن صفحتطوير.نقرئخفيمنسدلة ()؛
ئعلن صفحتطوير.عندبحتبطاقات (حدت ح، عنصرئدخال عنصر) ك وعد؛
ئعلن صفحتطوير.عندتحجيمعاينة (منوع ح)؛
ئعلن صفحتطوير.ئعرضطفنشر ()؛
ئعلن صفحتطوير.حممل () ك وعد؛
ئعلن صفحتطوير.نقرحججمعاينة ()؛
ئعلن صفحتطوير.عندنقرمرشح (حدتمخصص ح؟) ك منطق؛
ئعلن صفحتطوير.عندنهتحجيمعاينة (منوع ح)؛
ئعلن صفحتطوير.صيير ()؛
ئعلن صفحتطوير.شغغلمشروع () ك وعد؛
ئعلن صفحتطوير.عندبدتحجيمعاينة (منوع ح)؛
ئعلن صفحتطوير.عندنقرئيداع ()؛
ئعلن صفحتطوير.عندئختيارملف ()؛
ئعلن مدخل ()؛

ئعلن صفحتطوير ():
	يمدد مكون()؛
	يملك نصية عرضبطاقات؛
	يملك نصية عرضئفريم؛
تم

ئعلن صفحتطوير.نقرئنشئبطاقة ():
	مشهدبطاقة مشهبط = المستند.ئستعلم((<مشهدبطاقة>))؛
	ئدا مشهبط.حالة.ضاهر:
		مشهبط.عندغلقبطاقة()؛
	تم
	مشهبط.خدحالة({
		ضاهر: صحيح،
		بطاقة: عدم
	})؛
تم

ئعلن صفحتطوير.نقرقائموحدة ():
	مكون مشهدوحدات = المستند.ئستعلم((<مشهدوحدات>))؛
	مشهدبطاقة مشهبط = المستند.ئستعلم((<مشهدبطاقة>))؛
	عنصر عنص = مشهدوحدات.ئستعلم('.ط-كامل')؛
	ئدا عنص.ئسمصنف.يتضمن('مخفي'):
		عنص.ئسمصنف = عنص.ئسمصنف.عووض('مخفي'، 'مرئي')؛
		ئدا مشهبط.حالة.ضاهر:
			مشهبط.عندغلقبطاقة()؛
		تم
	وئلا:
		عنص.ئسمصنف = عنص.ئسمصنف.عووض('مرئي'، 'مخفي')؛
	تم
تم

ئعلن صفحتطوير.عندنقرنشر ():
	هدا.نقرئخفيمنسدلة()؛
	#تحقق من وجود تعديلين غير مودوعين
	مختطمشروع مشروعي = مجالمشروع.مشروعحالي؛
	ئدا مشروعي.ئاخرتعديل > مشروعي.ئاخرئيداع:
		نعملا نعلا = هدا.ئستعلم((<نعملا>))؛
		نعلا.خدحالة({
			رسالة: 'توجد تعديلات لم يتم ئيداعها. هل ترغب في الئيداع؟'،
			عندنعم: (دالة(): هدا.عندنقرئيداع())،
			عندلا: (دالة(): هدا.ئعرضطفنشر())،
			ئيقو: 'تنبيه-دائرة'
		})؛
	وئلا:
		هدا.ئعرضطفنشر()؛
	تم
تم

ئعلن صفحتطوير.نقرعرضمنسدلة ():
	عنصرئشتمل منسدلة = هدا.ئستعلم('.منسدلة')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('مخفي'، 'مرئي')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('بلاعرض'، 'فلكس')؛
تم

ئعلن صفحتطوير.نقرئخفيمنسدلة ():
	عنصرئشتمل منسدلة = هدا.ئستعلم('.منسدلة')؛
	منسدلة.ئسمصنف = منسدلة.ئسمصنف.عووض('مرئي'، 'مخفي')؛
تم

ئعلن صفحتطوير.عندبحتبطاقات (حدت ح، عنصرئدخال عنصر) ك وعد:
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوحد = المستند.ئستعلم((<مشهدوحدات>))؛
	نصية مبحت = عنصر.قيمة؛
	مختطبطاقة[] بطا = ريتما مجالبطاقة.ئبحتبطاقات(قاعدب.قابمحلي، مبحت)؛
	
	ئدا مشهوحد.حالة.مساروحدتحالية:
		مشهوحد.خدحالة({
			مساروحدتحالية: عدم
		})؛
	تم
	
	مشهبط.خدحالة({
		بطاقات: بطا
	})؛
تم

ئعلن صفحتطوير.عندتحجيمعاينة (منوع ح):
	#هدا = المستند
	صفحتطوير نفسه = المستند.ئستعلم((<صفحتطوير>))؛
	#عنصرئشتمل ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	عنصرئشتمل ئفريم = المستند.ئستعلم((<مشهدمعاينة>))؛
	مشهدبطاقات مشهبط = نفسه.ئستعلم((<مشهدبطاقات>))؛
	عنصرئشتمل جهتبطاقات = المستند.ردعنصربمعرف('جهتبطاقات')؛
	دع لمسات = ح['touches']؛
	عدد سئان؛
	ئدا لمسات:
		دع لمسة = لمسات[0]؛ #ئول نقطة لمس
		سئان = لمسة['clientX']؛
	وئلا:
		سئان = ح['clientX']؛
	تم
	ئفريم.سمة['width'] = "%{سئان}px"؛
	جهتبطاقات.سمة['width'] = "%{المستند.جسم.عرضداخلي - سئان}px"؛
تم

ئعلن صفحتطوير.ئعرضطفنشر ():
	طافية طاف = المستند.ردعنصربمعرف('طفخيارين')؛
	طاف.خدحالة({
		جسم: 'مشهدنشر'،
		عنوان: 'نشر المشروع'
	})؛
تم

ئعلن صفحتطوير.حممل () ك وعد:
	المستند.عنوان = مجالمشروع.مشروعحالي.ئسمشروع + ' - جني'؛
	#عنصرئشتمل ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	مشهدمعاينة ئفريم = المستند.ئستعلم((<مشهدمعاينة>))؛
	مشهدمرشحات مشهمرشح = هدا.ئستعلم((<مشهدمرشحات>))؛
	عنصرئدخال ئدخال = المستند.ردعنصربمعرف('ئدخبحتبطاقات')؛
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوحد = المستند.ئستعلم((<مشهدوحدات>))؛
	عنصرئشتمل قسمتحجيم = المستند.ردعنصربمعرف('قسمتحجيم')؛
	عنصرئشتمل جهتبطاقات = المستند.ردعنصربمعرف('جهتبطاقات')؛
	دع ئدخملف = هدا.ئستعلم('#ئدخرفعملف')؛
	
	# ئعادة توجيه الطرفية
	
	
	#تحميل المقاسين
	نصية سمعاينة = النافدة.تخزينجلسة.ردعنصر('جني_س_معاينة')؛

	ئدا (سمعاينة لا= عدم) وو (النافدة.عرضداخلي > 768):
		#لدينا تفضيل حجم معاينة محفوض ونحن لسنا علا شاشة صغيرة
		نصية[] شطرين = سمعاينة.قسسم(':')؛
		ئفريم.سمة['width'] = شطرين[0]؛
		جهتبطاقات.سمة['width'] = شطرين[1]؛
		هدا.عرضئفريم = شطرين[0]؛
		هدا.عرضبطاقات = شطرين[1]؛
	تم
	
	#كشف التمرير في الشاشات الصغيرة
	ئدا النافدة.عرضداخلي < 768:
		ئكشفتمرير(النافدة، دالة(نصية ئتج): (ئتج == 'يمين') وو هدا.نقرحججمعاينة() ك منطق )؛
	تم

	#مستمعيي الحدتين
	مشهمرشح.ئضفمستمعحدت('نقرمرشح'، دالة (حدتمخصص ح): هدا.عندنقرمرشح(ح))؛
	ئدخال.ئضفمستمعحدت('تركمفتاح'، دالة (حدت ح): هدا.عندبحتبطاقات(ح، ئدخال))؛
	مشهبط.ئضفمستمعحدت('حدفبطاقة'، دالة (حدتمخصص ح): هدا.شغغلمشروع())؛
	#مشهبط.ئضفمستمعحدت('حدفوحدة'، دالة (حدتمخصص ح): هدا.عندنقرمرشح())؛
	مشهبط.ئضفمستمعحدت('حفضبطاقة'، دالة (حدتمخصص ح): هدا.شغغلمشروع())؛
	#مشهبط.ئضفمستمعحدت('حفضبطاقة'، دالة (حدتمخصص ح): هدا.شغغلمشروع())؛
	قسمتحجيم.ئضفمستمعحدت('ضغطمئشر'، هدا.عندبدتحجيمعاينة ك وضيفة)؛
	قسمتحجيم.ئضفمستمعحدت('بدايتلمس'، هدا.عندبدتحجيمعاينة ك وضيفة)؛
	ئدخملف.ئضفمستمعحدت('تغيير'، هدا.عندئختيارملف ك وضيفة)؛
	مشهوحد.ئضفمستمعحدت('حدفوحدة'، دالة (حدتمخصص ح): هدا.عندنقرمرشح() وو هدا.شغغلمشروع() ك منطق)؛
	هدا.ئربطحدتين()؛
	
	#تسجيل العاملخدمة
	حاول:
		ريتما المتصفح.عاملخدمة.سججل(رابطملف: '/خادمتطوير.جني'، نطاق: encodeURI('/ئختبر'))؛
	ئستدرك:
		قل 'فشل تسجيل العاملخدمة'؛
	تم
	
	#تشغيل المعاينة
	هدا.شغغلمشروع()؛
تم

ئعلن صفحتطوير.نقرحججمعاينة ():
	هدا.نقرئخفيمنسدلة()؛
	# عرض/ئخفائ/تحجيم المعاينة
	عنصرئشتمل جهتبطاقات = المستند.ردعنصربمعرف('جهتبطاقات')؛
	#عنصرئشتمل ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	عنصرئشتمل ئفريم = المستند.ئستعلم((<مشهدمعاينة>))؛
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	عنصرئشتمل تروي = المستند.ردعنصربمعرف("ترويمعاينة")؛
	
	منطق شاشتكبيرة = النافدة.عرضداخلي > 768؛
	
	ئدا شاشتكبيرة:
		نصية نصعرض = ئفريم.سمة['width']؛
		ئدا نصعرض == هدا.عرضئفريم ئو نصعرض == '':
			# نحن في العرض العادي، نبددل عرض كبير
			ئفريم.سمة['width'] = '70%'؛
			جهتبطاقات.سمة['width'] = '30%'؛
		تم
		ئدا نصعرض == '70%':
			# نحن في العرض الكبير، نبددل 0
			ئفريم.سمة['width'] = '0px'؛
			جهتبطاقات.سمة['width'] = '100%'؛
			# تم نفتح المعاينة في نافدة جديدة
			النافدة.ئفتح("/ئختبر/%{مجالمشروع.مشروعحالي.ئسمشروع}"، 'جديد')؛
		تم
		ئدا نصعرض == '0px':
			# نحن 0، نبددل عرض عادي
			ئفريم.سمة['width'] = هدا.عرضئفريم؛
			جهتبطاقات.سمة['width'] = هدا.عرضبطاقات؛
		تم
		مشهبط.حددت()؛
	وئلا:
		# شاشة صغيرة
		# نحددت المعاينة
		هدا.شغغلمشروع()؛
		ئفريم.ئسمصنف = ئفريم.ئسمصنف.عووض("فوق-8"، "فوق-0")؛
		ئفريم.ئسمصنف = ئفريم.ئسمصنف.عووضكل("بلاعرض"، "")؛
		تروي.ئسمصنف = تروي.ئسمصنف.عووض("مخفي"، "")؛
	تم
تم

ئعلن صفحتطوير.عندنقرمرشح (حدتمخصص ح؟) ك منطق:
	مشهدبطاقات مشهبط = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوح = المستند.ئستعلم((<مشهدوحدات>))؛
	ئدا (ليس ح) ئو (ح.بيا == '1'): #الكل
		مشهوح.خدحالة({
			مساروحدتحالية: عدم
		})؛
		مشهبط.حممل()؛
	وئلا ئدا ح.بيا == '2': #مفتوحة مئخرا
		مشهبط.خدحالة({
			بطاقات: مشهبط.ئاخرمفتوح
		})؛
	وئلا ئدا ح.بيا == '4': #خطئين
		مشهبط.خدحالة({
			بطاقات: مشهبط.بطاخطئين
		})؛
	وئلا ئدا ح.بيا == '3': #تحديرين
		مشهبط.خدحالة({
			بطاقات: [] #غير مدعوم
		})؛
	تم
	ئرجع صحيح؛
تم

ئعلن صفحتطوير.عندنهتحجيمعاينة (منوع ح):
	#هدا = المستند
	صفحتطوير نفسه = المستند.ئستعلم((<صفحتطوير>))؛
	#عنصرئشتمل ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	عنصرئشتمل ئفريم = المستند.ئستعلم((<مشهدمعاينة>))؛
	مشهدبطاقات مشهبط = نفسه.ئستعلم((<مشهدبطاقات>))؛
	عنصرئشتمل قسمتحجيم = المستند.ردعنصربمعرف('قسمتحجيم')؛
	قسمتحجيم.ئسمصنف = قسمتحجيم.ئسمصنف.عووض('خلف-ئخضر-700'، 'خلف-ئحمر-700')؛
	#حدف الحدتين المسجلين وحفض الئحداتيات
	المستند.ئحدفمستمعحدت('تركمئشر'، نفسه.عندنهتحجيمعاينة ك وضيفة)؛
	المستند.ئحدفمستمعحدت('نقر'، نفسه.عندنهتحجيمعاينة ك وضيفة)؛
	المستند.ئحدفمستمعحدت('تحريكمئشر'، نفسه.عندتحجيمعاينة ك وضيفة)؛
	المستند.ئحدفمستمعحدت('تحريكلمس'، نفسه.عندتحجيمعاينة ك وضيفة)؛
	النافدة.تخزينجلسة.خدعنصر('جني_س_معاينة'، ئفريم.سمة['width'] + ':' + مشهبط.سمة['width'])؛
	نفسه.عرضبطاقات = مشهبط.سمة['width']؛
	نفسه.عرضئفريم = ئفريم.سمة['width']؛
	مشهبط.حددت()؛
تم

ئعلن صفحتطوير.صيير ():
	منطق شاشتكبيرة = النافدة.عرضداخلي > 768؛
	عدد طول؛
	ئدا شاشتكبيرة:
		طول = النافدة.طولداخلي - 60؛
	وئلا:
		طول = النافدة.طولداخلي - 30؛
	تم
	ئرجع (
		<قسم ئسم="ترويس" صنف="ئرصف-وسط مخطط-طولي فجوة-4 ضلصغ خلف-ئسود حشس-4 حشص-2 ئطار-رمادي-700 لون-ئسود ئطارت1">
			<قسم صنف="مخطط-عرضي فجوة-4">
				<قسم عندنقر="نقرقائموحدة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة نص-رمادي-300 مقوسكب تئشير:ئطار-ئبيض">
					<ئيقو ئسم="قائمة" صنف="نصكج"></ئيقو>
				</قسم>
				<رابط عنونت="/" صنف="نفسه-وسط">
					<صورة مصدر="/ملفا/شعار.svg" طول="30" صنف="ئصفف-ضمني-وسط"></صورة>
				</رابط>
				<ئدخال معرف="ئدخبحتبطاقات" نوع="نص" ستنب="ئبحت هنا" صنف="نصصج ئطاربلا خلف-ئسود نص-ئبيض يكبر حشيم-3 حشص-3" سمة="min-width:0"></ئدخال>
			
				<قسم صنف="مخطط فجوة-3">
					<قسم عندنقر="نقرئنشئبطاقة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة تئشير:خلف-رمادي-900 نص-رمادي-300 مقوسكب ئطار1 ئطار-رمادي-700 تئشير:ئطار-ئبيض حشس-2">
						<ئيقو ئسم="زائد" صنف="نصكج"></ئيقو>
					</قسم>
					<قسم عندنقر="نقرحججمعاينة" صنف="مت:مخطط-عرضي:هام بلاعرض ئصفف-وسط ئرصف-وسط مئشر-سبابة تئشير:خلف-رمادي-900 نص-رمادي-300 مقوسكب ئطار1 ئطار-رمادي-700 تئشير:ئطار-ئبيض حشس-2">
						<ئيقو ئسم="قاجانب-تمديد" صنف="نصكج"></ئيقو>
					</قسم>
					<قسم عندنقر="نقرعرضمنسدلة" صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط مئشر-سبابة تئشير:خلف-رمادي-900 نص-رمادي-300 مقوسكب ئطار1 ئطار-رمادي-700 تئشير:ئطار-ئبيض حشس-2">
						<ئيقو ئسم="سهم-تصفح-تحت" صنف="نصكج"></ئيقو>
					</قسم>
					<قسم صنف="مخطط-طولي فجوة-1 منسدلة موضعمطلق محورض-3000 خلف-رمادي-900 مقوسكج ئطار1 ئطار-رمادي-700 ضلكج نهاية-4 فوق-14 مخفي حشص-3 مقوسكب ع-36">
						<قسم عندنقر="نقرئخفيمنسدلة" صنف="موضعتابت فوق-0 يمين-0 ستارمنسدل ط-شاشة ع-شاشة خلف-شفاف محورض30">
						</قسم>
						<قسم صنف="مخطط-طولي فجوة-2">
							<قسم صنف="مخطط-عرضي يلتف فجوة-2 نص-ئبيض ئصفف-وسط ئرصف-وسط محورض-3000">
								<ئيقو ئسم="رفع-مربع" عندنقر="عندنقرتحميل" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض-3000"></ئيقو>
								<ئيقو ئسم="ئحجية" عندنقر="عندنقرمكون" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض-3000"></ئيقو>
							</قسم>
							<قسم صنف="مخطط-عرضي يلتف فجوة-2 نص-ئبيض ئصفف-وسط ئرصف-وسط محورض-3000">
								<ئيقو ئسم="جيت-طلب-سحب" عندنقر="عندنقرئيداع" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض-3000"></ئيقو>
								<ئيقو ئسم="ئنترنت" عندنقر="عندنقرنشر" صنف="نصكجج مئشر-سبابة مقوسكب حش-3 تئشير:خلف-رمادي-700 محورض-3000"></ئيقو>
							</قسم>
						</قسم>
					</قسم>
				</قسم>
			</قسم>
		</قسم>
		
		<قسم صنف="مخطط-عرضي">
			<قسم معرف="جهتبطاقات" صنف="خلف-حيادي-900 مخطط-طولي ع-كامل مت:ع-ئلاربع موضع-نسبي">
				<مشهدبطاقة صنف="موضع-تابت مت:موضع-مطلق فوق-0 ع-كامل محورض-1000"></مشهدبطاقة>
				<قسم صنف="موضع-مطلق ع-كامل">
					<مشهدمرشحات></مشهدمرشحات>
					<مشهدبطاقات></مشهدبطاقات>
				</قسم>
			</قسم>
			<قسم معرف="قسمتحجيم" صنف="فوق-0 ع-3 ئدنا مئشر-تحجيم-عرضي خلف-ئحمر-700 بلاعرض مت:بلوك:هام"></قسم>
			<مشهدمعاينة></مشهدمعاينة>
		</قسم>
		
		<مشهدوحدات></مشهدوحدات>
		
		<ئدخال type="file" صنف="بلاعرض" معرف="ئدخرفعملف"></ئدخال>
		<طافية معرف="طفخيارين" عنصر-بطاقة="خلف-حيادي-900" صنف="داكن" تابتة عنصر-عنوان="نصصغ"></طافية>
		<نعملا صنف="داكن" نبرة="ئحمر-700" عنصر-عنوان="نصصغ" عنصر-رسالة="نصصج ئرتف-سطر-واسع:هام"></نعملا>
	)؛
تم

ئعلن صفحتطوير.شغغلمشروع () ك وعد:
	#دع ئفريم = المستند.ردعنصربمعرف('ئفريمعاينة')؛
	مشهدمعاينة ئفريم = المستند.ئستعلم((<مشهدمعاينة>))؛
	مشهدمرشحات مشهمرشح = هدا.ئستعلم((<مشهدمرشحات>))؛
	مشهدبطاقات مشهبطا = هدا.ئستعلم((<مشهدبطاقات>))؛
	مشهدوحدات مشهوح = هدا.ئستعلم((<مشهدوحدات>))؛
	
	ئفريم.شغغلتحميل()؛
	ئفريم.ئمسح()؛
	
	ريتما مشهوح.حممل()؛
	ئدا مشهوح.حالة.مساروحدتحالية:
		ريتما مشهوح.حمملحالية()؛ #نحددت المشهدوحدات لئن تشغيل المشروع ينتج عن تعديلين
	وئلا:
		هدا.عندنقرمرشح()؛
	تم
	
	منطق هلفشل = خطئ؛
	حاول:
		ريتما ترجم("%{مجالمشروع.مشروعحالي.ئسمشروع}.جني")؛
		مجالبطاقة.ئفرغخطئين()؛
		مشهمرشح.خدحالة({
			كمخطئ: 0
		})؛
		مشهبطا.خدبطاخطئين([])؛
	ئستدرك ف ك فشل:
		قل ف؛
		منوع فش = ف؛
		هلفشل = صحيح؛
		
		منوع[] خطئين = فش['خطئين']؛
		لكل فخطئ في خطئين:
			ئفريم.ئكتبسطر(فخطئ['رسالة'] + '<br> &ensp;&ensp;في ' + فخطئ['بطاقة'])؛
		تم
		ئفريم.ئفتحطرفية()؛
		
		مختطبطاقة[] بطاخطئين = ريتما مجالبطاقة.بطاقاتمنخطئين(قاعدب.قابمحلي، فش)؛
		مشهمرشح.خدحالة({
			كمخطئ: فش['كمخطئ']
		})؛
		مشهبطا.خدبطاخطئين(بطاخطئين)؛
	تم
	
	مشهبطا.حددت()؛
	ئدا ليس هلفشل:
		ريتما ئستولدجيسس()؛
		ئفريم.شغغلرابط("/ئختبر/%{مجالمشروع.مشروعحالي.ئسمشروع}")؛
	تم
تم

ئعلن صفحتطوير.عندبدتحجيمعاينة (منوع ح):
	#هدا = قسمتحجيم
	عنصرئشتمل قسمتحجيم = المستند.ردعنصربمعرف('قسمتحجيم')؛
	قسمتحجيم.ئسمصنف = قسمتحجيم.ئسمصنف.عووض('خلف-ئحمر-700'، 'خلف-ئخضر-700')؛
	صفحتطوير نفسه = المستند.ئستعلم((<صفحتطوير>))؛
	المستند.ئضفمستمعحدت('تركمئشر'، نفسه.عندنهتحجيمعاينة ك وضيفة)؛
	المستند.ئضفمستمعحدت('نقر'، نفسه.عندنهتحجيمعاينة ك وضيفة)؛
	المستند.ئضفمستمعحدت('نهايتلمس'، نفسه.عندنهتحجيمعاينة ك وضيفة)؛
	المستند.ئضفمستمعحدت('تحريكمئشر'، نفسه.عندتحجيمعاينة ك وضيفة)؛
	المستند.ئضفمستمعحدت('تحريكلمس'، نفسه.عندتحجيمعاينة ك وضيفة)؛
تم

ئعلن صفحتطوير.عندنقرئيداع ():
	#طافية طاف = هدا.ئستعلم((<طافية>))؛
	هدا.نقرئخفيمنسدلة()؛
	طافية طاف = المستند.ردعنصربمعرف('طفخيارين')؛
	طاف.خدحالة({
		جسم: 'مشهدئيداع'،
		عنوان: 'ئيداع التعديلين'
	})؛
تم

ئعلن صفحتطوير.عندنقرمكون ():
	مشهدوحدات مشهوح = هدا.ئستعلم((<مشهدوحدات>))؛
	هدا.نقرئخفيمنسدلة()؛
	طافية طاف = المستند.ردعنصربمعرف('طفخيارين')؛
	طاف.خدحالة({
		جسم: 'مشهدمكون'،
		عنوان: 'ئضافة مكون'
	})؛
	طاف.ئضفمستمعحدت('ئغلاقطافية'، دالة(): هدا.شغغلمشروع())؛
تم

ئعلن صفحتطوير.عندنقرتحميل ():
	هدا.نقرئخفيمنسدلة()؛
	دع ئدخملف = هدا.ئستعلم('#ئدخرفعملف')؛
	ئدخملف['click']()؛
تم

ئعلن صفحتطوير.عندئختيارملف () ك وعد:
	غمازة غم = المستند.ئستعلم((<غمازة>))؛
	صفحتطوير صفح = المستند.ئستعلم((<صفحتطوير>))؛
	مشهدوحدات مشهوح = المستند.ئستعلم((<مشهدوحدات>))؛
	
	ملف[] ملفين = هدا['files']؛
	لكل ملفي في ملفين:
		ئدا ملفي.حجم > 2000000: #لايقبل ملفين ئكبر من 2ميجا
			غم.خدحالة({
				رسالة: 'لايقبل ملفين ئكبر من 2 ميجا'
			})؛
			ئرجع؛
		تم
		
		مختطوحدة وح = ريتما مجالوحدة.ئنشئوحدتملف(ملفي)؛
		قل وح؛
		ريتما مجالوحدة.ئحفض(قاعدب.قابمحلي، وح)؛		
	تم
	
	مشهوح.حممل()؛
	صفح.شغغلمشروع()؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('صفحتطوير'، صفحتطوير)؛
تم
ئورد jlexer ك جلكسر من './jlexer.js'؛
ئورد Image من '//'؛
ئورد حدت، عنصرمخصص، مكون، عنصرئشتمل، حدتمخصص؛
ئورد المستند من مستند؛
ئورد رجكس؛
ئورد مجال من مصفوفة؛
ئورد صحيحمننصية من عدد؛
ئورد النافدة من نافدة؛
ئورد مكون.نعملا؛
ئورد الحافضة من متصفح؛

ئعلن صورة ():
	يختصر Image؛
تم

ئعلن مشهدكود ():
	يمدد مكون ()؛
	
	يملك مركب { نصية مصدر } حالة = {
		مصدر: ''
	}؛
	
	يملك عدد محدد = 0-1؛
	يملك عدد تمريرفوق؛
	يملك عدد[] سطرمحددين؛
تم

ئعلن مشهدكود.هييئمصدر (نصية سطر) ك نصية:
	نصية مهيئ = ''؛
	دع ليكسر = جلكسر.lexer؛

	ليكسر.setInput(سطر)؛
	
	دع توكن = ليكسر.lex()؛
	طالما توكن لا= 1:
		نصية مفردة = ليكسر.match؛
		مفردة = مفردة.عووضكل('<'، '&lt;').عووضكل('>'، '&gt;')؛
		ئدا توكن == 'KEYWORD':
			مهيئ = مهيئ + (<حيز صنف="جني-مفتاحية">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'OPERATOR':
			مهيئ = مهيئ + (<حيز صنف="جني-معامل">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'SYMBOL':
			مهيئ = مهيئ + (<حيز صنف="جني-رمز">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'STRING':
			مهيئ = مهيئ + (<حيز صنف="جني-تنصيص">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'NUMBER':
			مهيئ = مهيئ + (<حيز صنف="جني-رقم">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'COMMENT':
			مهيئ = مهيئ + (<حيز صنف="جني-تعليق">%{مفردة}</حيز>)؛
		وئلا:
			مهيئ = مهيئ + مفردة؛
		تم
		توكن = ليكسر.lex()؛
	تم
	ئرجع مهيئ؛
تم

ئعلن مشهدكود.نضضف (نصية[] مقسم):
	#نصية[] مقسم = مصدري.قسسمرجكس(رجكس('\r?\n'))؛
	نصية[] سطرين = []؛
	عدد كمفرغ = 0؛
	عدد د = 0؛
	لكل ع في مجال(0، مقسم.طول-1):
		ئدا د > 0 وو ع < د:
			ئستمر؛ #تخطي ئستطواف السطرين المعالجين سابقا
		تم
		نصية سطري = مقسم[ع]؛
		سطري = سطري.قللم()؛ #حدف المسافات من الجانبان
		ئدا (سطري.يبدئب('وئلا') ئو سطري.يبدئب('ئستدرك') ئو (سطري.قللم() == 'تم') ئو سطري.يبدئب('}') ئو سطري.يبدئب(')') ئو سطري.يبدئب('"]')) وو (كمفرغ > 0):
			كمفرغ = كمفرغ - 1؛
		تم
		نصية طابة = '\t'؛
		نصية فرغ = طابة.كرر(كمفرغ)؛
		سطري = فرغ + سطري؛
		ئدا (ليس سطري.يبدئب('#')) وو (سطري.ينتهيب(':') ئو سطري.ينتهيب('{') ئو سطري.ينتهيب('[') ئو سطري.ينتهيب('(')):
			كمفرغ = كمفرغ + 1؛
		تم
		ئدا سطري.قللم().يبدئب('<'):
			#ئستطواف السطرين حتا الئغلاق
			كمفرغ = كمفرغ - 1؛
			منطق هلئولا = صحيح؛ #هل هده ئول مصادفة لوسم مغلق
			لكل غ في مجال(ع، مقسم.طول-1):
				نصية سطري2 = مقسم[غ]؛
				سطري2 = سطري2.قللم()؛
				ئدا سطري2 == '': #سطر فارغ لا نفعل شيئا
					سطرين.ئدفع(سطري2)؛
					ئستمر؛
				تم
				ئدا سطري2 == ')؛':
					د = غ؛
					ئكسر؛
				تم
				ئدا سطري2.يبدئب('</'):
					ئدا هلئولا:
						هلئولا = خطئ؛
					وئلا:
						كمفرغ = كمفرغ - 1؛
					تم
				وئلا ئدا سطري2.يبدئب('<'):
					ئدا ليس هلئولا:
						هلئولا = صحيح؛
					وئلا:
						كمفرغ = كمفرغ + 1؛
					تم
				تم
				فرغ = طابة.كرر(كمفرغ)؛
				ئدا ليس سطري2.يبدئب('</') وو سطري2.يتضمن('</'):
					كمفرغ = كمفرغ - 1؛
				تم
				سطري2 = فرغ + سطري2؛
				سطرين.ئدفع(سطري2)؛
				د = غ؛
			تم
		وئلا:
			سطرين.ئدفع(سطري)؛
		تم	
	تم
	
	ئرجع سطرين.ئدمج('\n')؛
تم

ئعلن مشهدكود.حددسطر (عدد ع):
	# حفض موضع التمرير
	عنصرئشتمل بفائض = هدا.ئستعلم('.قسمفائض')؛
	هدا.تمريرفوق = بفائض.مررئعلا؛
	
	# ئفراغ السطرين المحددين
	هدا.سطرمحددين = []؛
	
	عنصرئشتمل[] عنصرين = هدا.ئستعلمكل((<منسق>))؛
	ئدا ع < 0:
		ع = عنصرين.طول - 1؛
	تم
	هدا.محدد = ع؛
	عنصرئشتمل منسق = عنصرين[ع]؛
	لكل عنص في عنصرين:
		عنص.ئسمصنف = عنص.ئسمصنف.عووضكل(' خلف-حيادي-600'، '')؛
	تم
	منسق.ئسمصنف = منسق.ئسمصنف.قللمنهاية() + ' خلف-حيادي-600'؛
تم

ئعلن مشهدكود.حددسطرئخير ():
	هدا.حددسطر(0-1)؛
	عنصرئشتمل بفائض = هدا.ئستعلم('.قسمفائض')؛
	هدا.تمريرفوق = بفائض.طولتمرير؛
تم

ئعلن مشهدكود.ئعرضخصاسطر (عنصرئشتمل منسق):
	منطق شاشتكبيرة = النافدة.عرضداخلي > 768؛
	عنصرئشتمل خصاسطر = هدا.ئستعلم('#خصاسطر')؛
	عنصرئشتمل خصاسطرجديد = هدا.ئستعلم('#خصاسطرجديد')؛
	
	#ئخفائها ئدا كانت ضاهرة
	خصاسطر.ئسمصنف = خصاسطر.ئسمصنف.عووض('مرئي'، 'مخفي')؛
	خصاسطرجديد.ئسمصنف = خصاسطرجديد.ئسمصنف.عووض('مرئي'، 'مخفي')؛
	
	نصية محتوا = منسق.نصداخلي.قللم()؛
	
	ئدا محتوا == '':
		#نعرض خصاسطرجديد لئن السطر فارغ
		خصاسطر = خصاسطرجديد؛
	تم
	
	خصاسطر.ئسمصنف = خصاسطر.ئسمصنف.عووض('مخفي'، 'مرئي')؛
	عدد كمناقص = 80 ئدا شاشتكبيرة وئلا 20؛
	خصاسطر.سمة['top'] = "%{منسق.ردئطارمشهد().فوق - كمناقص}px"؛ 
تم


ئعلن مشهدكود.عندنقرسطر (عنصرئشتمل منسق، حدت ح؟):
	نصية[] سطرين = هدا.حالة.مصدر.قسسم('\n')؛
	عدد ع = صحيحمننصية(منسق.ردخاصية('بيا-سطر'))؛
	
	هدا.حددسطر(ع)؛
	نصية قيمة = سطرين[ع] ئو ''؛
	قيمة = قيمة.قللم()؛
	هدا.ئطلقحدت(حدتمخصص('تحديدسطر'، {يتصاعد: صحيح، بيا: {قيمة}}))؛
	
	هدا.ئعرضخصاسطر(منسق)؛
تم


ئعلن مشهدكود.حددبسحب (حدت ح):
	ح.ئمنعمفترض()؛
	عنصرئشتمل سطر = ح.هدف؛
	عدد ع = صحيحمننصية(سطر.ردخاصية('بيا-سطر'))؛
	ئدا ليس هدا.سطرمحددين.يتضمن(ع):
		ئدا ليس هدا.سطرمحددين.طول:
			# هدا ئول سطر في سلسلة التحديد
			#عنصرئشتمل محدد = هدا.ئستعلم('.خلف-حيادي-600')؛
			#محدد.ئسمصنف = محدد.ئسمصنف.عووض(' خلف-حيادي-600'، '')؛
			#هدا.ئعرضخصاسطر(سطر)؛
			ئدا ع لا= هدا.محدد:
				هدا.سطرمحددين.ئدفع(هدا.محدد)؛
			تم
			هدا.عندنقرسطر(سطر)؛
		تم
		ئدا (ليس هدا.سطرمحددين.طول) ئو (هدا.سطرمحددين.يتضمن(ع-1)):
			سطر.ئسمصنف = سطر.ئسمصنف.قللمنهاية() + ' خلف-حيادي-600'؛
			هدا.سطرمحددين.ئدفع(ع)؛
		تم
	تم
تم


ئعلن مشهدكود.حددت ():
	هدا.حددتمشهد()؛
	هدا.ئربطحدتين()؛
	
	# ئسترجاع التمرير
	عنصرئشتمل بفائض = هدا.ئستعلم('.قسمفائض')؛
	بفائض.مررئعلا = هدا.تمريرفوق؛
	
	# التحديد المتعدد علا السطر المحدد
	عنصرئشتمل[] سطرين = هدا.ئستعلمكل('.سطركود')؛
	منوع ص = صورة()؛

	لكل سطر في سطرين:
		سطر.ئضفمستمعحدت('بدايتسحب'، دالة(حدت ح): (ح ك منوع).dataTransfer.setDragImage(ص، 0، 0))؛
		سطر.ئضفمستمعحدت('سحبفوق'، دالة(حدت ح): هدا.حددبسحب(ح))؛
		#سطر.ئضفمستمعحدت('ئفلات'، دالة(حدت ح): هدا.ئنهحددبسحب(ح))؛
	تم
	
	#هدا.حددسطرئخير()؛
تم

ئعلن مشهدكود.عددلسطرمتعدد (نصية[] سطرجديدين):
	نصية[] سطرين = هدا.حالة.مصدر.قسسمرجكس(رجكس('\r?\n'))؛
	ئدا ليس هدا.محدد:
		هدا.حددسطرئخير()؛
	تم
	
	سطرين.ئجدل(هدا.محدد، 1، سطرجديدين)؛
	
	نصية مصدرنضيف = هدا.نضضف(سطرين)؛
	هدا.خدحالة({
		مصدر: مصدرنضيف
	})؛
تم

ئعلن مشهدكود.عددلسطر (نصية سطر، عدد ئين = 1):
	نصية[] سطرين = هدا.حالة.مصدر.قسسمرجكس(رجكس('\r?\n'))؛
	ئدا هدا.محدد < 0:
		هدا.حددسطرئخير()؛
	تم
	ئدا ئين == 1:
		# تعديل
		سطرين[هدا.محدد] = سطر؛
	تم
	ئدا ئين == 0:
		# يدرج فوق
		سطرين.ئجدل(هدا.محدد، 0، [سطر])؛
	تم
	ئدا ئين == 2:
		# يدرج تحت
		عدد غ = هدا.محدد + 1؛
		سطرين.ئجدل(غ، 0، [سطر])؛
	تم
	
	نصية مصدرنضيف = هدا.نضضف(سطرين)؛
	هدا.خدحالة({
		مصدر: مصدرنضيف
	})؛
تم

ئعلن مشهدكود.سطرجديد ():
	هدا.عددلسطر('\n'، 2)؛
	عدد ع = هدا.محدد + 1؛
	هدا.حددسطر(ع)؛
	عنصرئشتمل[] سطرين = هدا.ئستعلمكل('.سطركود')؛
	عنصرئشتمل سطري = سطرين[هدا.محدد]؛
	هدا.ئطلقحدت(حدتمخصص('تحديدسطر'، {يتصاعد: صحيح، بيا: {قيمة: ''}}))؛	
	هدا.ئعرضخصاسطر(سطري)؛
تم

ئعلن مشهدكود.ئحدفمحددين ():
	نصية[] سطرين = هدا.حالة.مصدر.قسسمرجكس(رجكس('\r?\n'))؛
	عدد تحديد؛
	
	ئدا هدا.سطرمحددين.طول:
		تحديد = هدا.سطرمحددين[هدا.سطرمحددين.طول-1]؛
		سطرين.ئجدل(هدا.سطرمحددين[0]، هدا.سطرمحددين.طول)؛
	وئلا:
		تحديد = هدا.محدد؛
		سطرين.ئجدل(هدا.محدد، 1)؛
	تم
	
	نصية مصدرنضيف = هدا.نضضف(سطرين)؛
	هدا.خدحالة({
		مصدر: مصدرنضيف
	})؛
	هدا.ئطلقحدت(حدتمخصص('تعديلمصدر'، {يتصاعد: صحيح}))؛
	
	# تحديد السطر الموالي لئاخر محدوف
	عنصرئشتمل[] عنصرسطرين = هدا.ئستعلمكل('.سطركود')؛
	عنصرئشتمل سطري = عنصرسطرين[تحديد]؛
	هدا.عندنقرسطر(سطري)؛
تم

ئعلن مشهدكود.نقرقمامة ():
	ئدا هدا.سطرمحددين.طول:
		نعملا نعمل = المستند.ئستعلم((<نعملا>))؛
		نعمل.خدحالة({
			رسالة: "سيتم حدف %{هدا.سطرمحددين.طول} سطر، هل متئكد؟"،
			عندنعم: (دالة(): هدا.ئحدفمحددين())،
			ئيقو: 'تنبيه-دائرة'
		})؛
	وئلا:
		هدا.ئحدفمحددين()؛
	تم
	
تم

ئعلن مشهدكود.نقرنسخ ():
	نصية منسوخ؛
	نصية[] سطرين = هدا.حالة.مصدر.قسسمرجكس(رجكس('\r?\n'))؛
	
	ئدا هدا.سطرمحددين.طول:
		نصية[] منسوخين؛
		لكل ع في هدا.سطرمحددين:
			منسوخين.ئدفع(سطرين[ع])؛
		تم
		منسوخ = منسوخين.ئدمج('\n')؛
		
		# تحديد ئول سطر منسوخ
		عدد تحديد = هدا.سطرمحددين[0]؛
		عنصرئشتمل[] عنصرسطرين = هدا.ئستعلمكل('.سطركود')؛
		عنصرئشتمل سطري = عنصرسطرين[تحديد]؛
		هدا.عندنقرسطر(سطري)؛
	وئلا:
		منسوخ = سطرين[هدا.محدد]؛
	تم
	
	الحافضة.ئكتبنص(منسوخ)؛
تم

ئعلن مشهدكود.نقرمقص ():
	هدا.نقرنسخ()؛
	هدا.نقرقمامة()؛
تم


ئعلن مشهدكود.نقرلصق () ك وعد:
	نصية منسوخ = ريتما الحافضة.ئقرئنص()؛
	نصية[] منسوخين = منسوخ.قسسم('\n')؛
	هدا.عددلسطرمتعدد(منسوخين)؛
تم

ئعلن مشهدكود.صيير ():
	نصية[] سطرين = هدا.حالة.مصدر.قسسم('\n')؛
	عدد ع؛
	ئرجع (
		<قسم صنف="ط-كامل فائض-تلقا فائض-لمسي فائض-لمس قسمفائض">
			<قسم معرف="خصاسطر" صنف="شفافية-60 تئشير:شفافية-100 مخفي مخطط-عرضي فجوة-2 موضع-مطلق يسار-3">
				<ئيقو ئسم="نسخ" عندنقر="نقرنسخ" صنف="ئحمر-700 تئشير:نصكج نصكب"></ئيقو>
				<ئيقو ئسم="مقص" عندنقر="نقرمقص" صنف="ئحمر-700 تئشير:نصكج نصكب"></ئيقو>
				<ئيقو ئسم="قمامة" عندنقر="نقرقمامة" صنف="ئحمر-700 تئشير:نصكج نصكب"></ئيقو>
			</قسم>
			<قسم معرف="خصاسطرجديد" صنف="شفافية-60 تئشير:شفافية-100 مخفي مخطط-عرضي فجوة-2 موضع-مطلق يسار-3">
				<ئيقو ئسم="ئلصق-حافضة" عندنقر="نقرلصق" صنف="ئحمر-700 تئشير:نصكج نصكب"></ئيقو>
				<ئيقو ئسم="قمامة" عندنقر="نقرقمامة" صنف="ئحمر-700 تئشير:نصكج نصكب"></ئيقو>
			</قسم>
			<تكرار لكل="ع" في="مجال\(0, سطرين.طول-1\)">
				<حيز صنف="رقمتضليل">%{ع+1}</حيز>
				<شرط>
					<عند تحقق="هدا.محدد == ع">
						<منسق draggable="true" عندنقر="عندنقرسطر" بيا-سطر="%{ع}" صنف="سطركود سطرمحدد سطرتضليل مئشر-سبابة تئشير:خلف-حيادي-600 خلف-حيادي-600">%{هدا.هييئمصدر\(سطرين[ع]\)}</منسق>
					</عند>
					<عند غيره>
						<منسق draggable="true" عندنقر="عندنقرسطر" بيا-سطر="%{ع}" صنف="سطركود سطرتضليل تئشير:خلف-حيادي-600 مئشر-سبابة">%{هدا.هييئمصدر\(سطرين[ع]\)}</منسق>
					</عند>
				</شرط>
			</تكرار>
			<حيز صنف="رقمتضليل">%{سطرين.طول+1}</حيز>
			<منسق عندنقر="عندنقرسطر" بيا-سطر="%{سطرين.طول}" صنف="%{'خلف-حيادي-600' ئدا هدا.محدد == سطرين.طول وئلا ''} سطرتضليل مئشر-سبابة تئشير:خلف-حيادي-600"></منسق>
		</قسم>
		<سمة>
			.رقمتضليل {
				color: #e8eaed;
				line-height:22px;
				font-size:10px;
				float: right;
				text-align: left;
				color: gray;
				width: 16px;
				max-width: 16px;
			}
			.سطرتضليل {
				color: #e8eaed;
				-moz-tab-size : 3;
				-o-tab-size : 3;
				tab-size : 3;
				line-height:22px;
				height:22px;
				font-size:10px;
				letter-spacing:1.4px;
				scrollbar-height: none;
				margin: 0;
				padding: 0;
				padding-right: 24px;
				width: fit-content;
				min-width: 100%;
			}
			.جني-مفتاحية {
				color: #dfc47d;
				font-weight: bold;
			}
			.جني-معامل {
				color: #EC4D37;
				font-weight: bold;
			}
			.جني-تنصيص {
				color: #cc9393;
			}
			.جني-رقم {
				color: #8CD0D3;
			}	
			.جني-شباك {
				background-color: #f8f9fa;
				padding-left: 5px;
				padding-right: 5px;
			}
			.جني-تعليمة {
				background-color: rgba\(50,51,55,255\);
				color: lightslategray;
			}
			.جني-تعليق{
				color: #7f9f7f;
			}
			.جني-ئشتمل {
				background-color: gray
			}
			.جني-رمز {
				color: #9f9d6d;
				font-weight: bold;
			}
		</سمة>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مشهدكود'، مشهدكود)؛
تم

ئورد الكل ك سياق من سياق؛
ئورد مصفبلش8، مفككترميز؛
ئورد ئجلب، جواب، ترويسة؛
ئورد مجال من مصفوفة؛
ئورد رجكس؛
ئورد الكل من جيزن؛

ئعلن هلرابطصحيح (نصية رابط) ك نصية[]:
	رجكس رجك = رجكس('github\\.com\\/([^\\/]+)\\/([^\\/]+)')؛
	نصية[] طباقين = رابط.طابق(رجك)؛
	
	ئدا ليس طباقين:
		ئرجع عدم؛ #صيغة رابط غير صحيحة
	وئلا:
		ئرجع طباقين؛
	تم
تم

ئعلن ردمعلومستودع (نصية رابط، نصية توكن) ك وعد مركب {نصية مالك، نصية مستودع، نصية وصف، نصية فرعمفترض}:
	نصية[] طباقين = هلرابطصحيح(رابط)؛
	
	ئدا ليس طباقين:
		ئرجع عدم؛
	تم
	
	نصية مالك = طباقين[1]؛
	نصية مستودع = طباقين[2]؛
	
	#ئستعلام برماجة جيتهب
	جواب جو = ريتما ئجلب(
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	
	ئدا جو.ناجح:
		دع نتج = ريتما جو.جيزن()؛
		ئرجع {
			مالك،
			مستودع،
			وصف: نتج['description']،
			فرعمفترض: نتج['default_branch']
		}؛
	وئلا:
		ئرجع عدم؛
	تم
تم

ئعلن ردجميعملفات (نصية توكن، نصية مالك، نصية مستودع، نصية فرع) ك وعد مركب[]:
	#جلب معرف ئاخر ئيداع
	جواب جوئاخرئيداع = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/refs/heads/_{فرع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بيائاخرئيداع = ريتما جوئاخرئيداع.جيزن()؛
	نصية معرفئيداع = بيائاخرئيداع['object'].sha؛
	
	#جلب معرف الشجرة
	جواب جوئيداع = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/commits/_{معرفئيداع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بيائيداع = ريتما جوئيداع.جيزن()؛
	نصية معرفشجرة = بيائيداع['tree'].sha؛
	
	#جلب شجرة ملفات المستودع
	جواب جوشجرة = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/trees/_{معرفشجرة}?recursive=1"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بياشجرة = ريتما جوشجرة.جيزن()؛
	منوع[] بيا = بياشجرة['tree']؛
	دع نتج = بيا.رششح(دالة (عنصر): عنصر['type'] == 'blob')؛
	ئرجع نتج؛
تم

ئعلن ئساس64ئلايوتف8 (ئساس64) ك نصية:
	نصية نصئتنائي = ئساس64.فكترميز64()؛
	عدد طول = نصئتنائي.طول؛
	
	مصفبلش8 بايتات = مصفبلش8(طول)؛
	لكل ع في مجال(0، طول):
		بايتات[ع] = نصئتنائي.ردرمزحرف(ع)؛
	تم
	
	مفككترميز مفك = مفككترميز('يوتف8')؛
	ئرجع مفك.فكترميز(بايتات)؛
تم

ئعلن ردمحتواملف (نصية توكن، نصية مالك، نصية مستودع، نصية مسار) ك وعد نصية:
	جواب جومحتوا = ريتما ئجلب(
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/contents/%{مسار}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بيامحتوا = ريتما جومحتوا.جيزن()؛
	ئرجع ئساس64ئلايوتف8(بيامحتوا['content'])؛
	#ئرجع (بيامحتوا['content'] ك نصية).فكترميز64()؛
تم

ئعلن ئودعملفات (نصية توكن، نصية مالك، نصية مستودع، نصية فرع، مركب[] {نصية مسار، نصية محتوا} تعديلات، نصية رسالة) ك وعد:
	#جلب معرف ئاخر ئيداع
	جواب جوئاخرئيداع = ريتما ئجلب(
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/git/refs/heads/%{فرع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بيائاخرئيداع = ريتما جوئاخرئيداع.جيزن()؛
	نصية معرفئاخرئيداع = بيائاخرئيداع['object'].sha؛
	
	#جلب معرف الشجرة
	جواب جوئيداع = ريتما ئجلب(
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/git/commits/%{معرفئاخرئيداع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")
		]
	)؛
	دع بيائيداع = ريتما جوئيداع.جيزن()؛
	نصية معرفشجرة = بيائيداع['tree'].sha؛
	
	#ئنشائ شجرة جديدة بالتعديلات
	جواب جوشجرجديدة = ريتما ئجلب(
		نوع: 'ئبعت'،
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/git/trees"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")،
			ترويسة('يرجع'، 'جيزن')
		]،
		جسم: جيزن.ئلانصية({
			base_tree: معرفشجرة،
			tree: تعديلات.طببق(دالة(تعديل): {
				path: تعديل.مسار،
				mode: '100644'، #نمط ملف
				type: 'blob'،
				content: تعديل.محتوا
			})
		})
	)؛
	
	دع بياشجرجديدة = ريتما جوشجرجديدة.جيزن()؛
	نصية معرفشجرجديدة = بياشجرجديدة['sha']؛
	
	#ئنشائ ئيداع جديد
	جواب جوئيداعجديد = ريتما ئجلب(
		نوع: 'ئبعت'،
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/git/commits"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")،
			ترويسة('يرجع'، 'جيزن')
		]،
		جسم: جيزن.ئلانصية({
			message: رسالة،
			tree: معرفشجرجديدة،
			parents: [معرفئاخرئيداع]
		})
	)؛
	
	دع بيائيداعجديد = ريتما جوئيداعجديد.جيزن()؛
	نصية معرفئيداعجديد = بيائيداعجديد['sha']؛
	
	#تحديت المرجع للئشارة ئلا الئيداع الجديد
	ريتما ئجلب(
		نوع: 'ئنقح'،
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/git/refs/heads/%{فرع}"،
		ترويسات: [
			ترويسة('تخويل'، "token %{توكن}")،
			ترويسة('يرجع'، 'جيزن')
		]،
		جسم: جيزن.ئلانصية({
			sha: معرفئيداعجديد
		})
	)؛
تم

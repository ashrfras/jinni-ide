ئورد jlexer ك جلكسر من './jlexer.js'؛
ئورد حدت، عنصرمخصص، مكون، حدتمخصص، عنصرئشتمل؛
ئورد سياق من سياق؛
ئورد مكون.غمازة؛
ئورد المستند من مستند؛
ئورد عنصرمربعنص؛
ئورد الحافضة من متصفح؛

ئعلن مشهدئدخال ():
	يمدد مكون()؛
	يملك مركب { منطق هلدكا، نصية رماز، عدد ئنتقال } حالة = {
		هلدكا: خطئ،
		ئنتقال: 1،
		رماز: ''
	}؛
	
	يملك نصية[] ئستكملعام = [
		'()'،
		'{}'،
		'&quot;&quot;'،
		"''"،
		'<>'،
		'[]'،
		'&lt;/&gt;'،
		'='،
		'؛'،
		':'،
		'%'،
		'،'،
		'+'،
		'-'،
		'×'،
		'÷'
	]؛
تم

ئعلن مشهدئدخال.هييئمصدر () ك نصية:
	نصية مهيئ = ''؛
	دع ليكسر = جلكسر.lexer؛

	ليكسر.setInput(هدا.حالة.رماز)؛
	
	دع توكن = ليكسر.lex()؛
	طالما توكن لا= 1:
		نصية مفردة = ليكسر.match؛
		مفردة = مفردة.عووضكل('<'، '&lt;').عووضكل('>'، '&gt;')؛
		ئدا توكن == 'KEYWORD':
			مهيئ = مهيئ + (<حيز صنف="جني-مفتاحية">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'OPERATOR':
			مهيئ = مهيئ + (<حيز صنف="جني-معامل">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'SYMBOL':
			مهيئ = مهيئ + (<حيز صنف="جني-رمز">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'STRING':
			مهيئ = مهيئ + (<حيز صنف="جني-تنصيص">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'NUMBER':
			مهيئ = مهيئ + (<حيز صنف="جني-رقم">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'COMMENT':
			مهيئ = مهيئ + (<حيز صنف="جني-تعليق">%{مفردة}</حيز>)؛
		وئلا:
			مهيئ = مهيئ + مفردة؛
		تم
		توكن = ليكسر.lex()؛
	تم
	ئرجع مهيئ؛
تم


ئعلن مشهدئدخال.نقرئستكمالعام (عنصرئشتمل عنص):
	نصية قيمة = عنص.ردخاصية('بيا-قيمة')؛
	عنصرمربعنص مربنص = هدا.ئستعلم((<مربعنص>))؛
	عدد بد = مربنص.بدتحديد؛
	عدد نه = مربنص.نهتحديد؛
	مربنص.قيمة = مربنص.قيمة.ردقطعة(0، بد) + قيمة + مربنص.قيمة.ردقطعة(نه)؛
	عدد كميزحف = قيمة.طول؛
	ئدا قيمة.طول > 1:
		كميزحف = كميزحف - 1؛
	تم
	هدا.خدحالة({
		رماز: مربنص.قيمة
	})؛
	مربنص = هدا.ئستعلم((<مربعنص>))؛
	(مربنص ك منوع).focus()؛
	مربنص.بدتحديد = بد + كميزحف؛
	مربنص.نهتحديد = بد + كميزحف؛
تم


ئعلن مشهدئدخال.صيير ():
	نصية ئستك؛
	ئرجع (
		<قسم معرف="ئستكمالعام" صنف="فجوة-3 فائض-تلقا فائض-لمسي ئبيض نصصجج حشتح-2 بلاعرض">
			<تكرار لكل="ئستك" في="هدا.ئستكملعام">
				<حيز بيا-قيمة="%{ئستك}" عندنقر="نقرئستكمالعام" صنف="مقوس ئطار-1 ئطار-رمادي-700 حش-2 حشس-3 مئشر-سبابة تئشير:خلف-حيادي-700">%{ئستك}</حيز>
			</تكرار>
		</قسم>
		<قسم صنف="مخطط-طولي خلف-حيادي-700 حش-2 مقوس">
			<قسم صنف="ط-14 ئقصط-14 فائضص-تلقا موضعنسبي">
				<مربعنص rows="4" ستنب="رماز" صنف="نصصج موضعمطلق ئبيض ع-كامل خلف-حيادي-700 ئطاربلا حش-0 هش-0 فائض-مخفي محورض-10" سمة="z-index:1;color:transparent;background:transparent;caret-color:white;resize:none;outline:none;line-height:22px;letter-spacing:1.4px;word-break:break-all;font-size:10px">%{هدا.حالة.رماز}</مربعنص>
				<منسق صنف="نصصج موضعمطلق حش-0 هش-0 ئبيض نص-يلتف فائض-مخفي محورض-0" سمة="z-index:0;text-wrap:wrap;line-height:22px;letter-spacing:1.4px;word-break:break-all;font-size:10px">%{هدا.هييئمصدر\(\)}</منسق>
			</قسم>
			<قسم صنف="مخطط-عرضي ئرصف-بين">
				<قسم صنف="مخطط-عرضي فجوة-1">
					<ئيقو عندنقر="عندنقرسطرجديد" ئسم="مربع-زائد-متقطع" صنف="مئشر-سبابة نصكج مقوسكامل ئبيض حشص-2 حشس-3"></ئيقو>
					<شرط>
						<عند تحقق="هدا.حالة.هلدكا">
							<ئيقو ئسم="جواهر-ملئ" عندنقر="عندنقردكا" صنف="مئشر-سبابة نصكج مقوسكامل ئحمر-700 حشص-2 حشس-3"></ئيقو>
						</عند>
						<عند غيره>
							<ئيقو ئسم="جواهر" عندنقر="عندنقردكا" صنف="مئشر-سبابة نصكج مقوسكامل ئبيض حشص-2 حشس-3"></ئيقو>
						</عند>
					</شرط>
				</قسم>
				<قسم صنف="مخطط-عرضي">
					<ئيقو ئسم="سهم-يسار" عندنقر="عندنقرئرسال" صنف="مئشر-سبابة ئبيض نصكج مقوسكامل حشص-2 حشس-3 تئشير:خلف-ئحمر-600"></ئيقو>
				</قسم>
			</قسم>
		</قسم>
	)؛
تم

ئعلن مشهدئدخال.عندنقرسطرجديد ():
	هدا.ئطلقحدت(حدتمخصص('سطرجديد'، {
		يتصاعد: صحيح
	}))؛
تم

ئعلن مشهدئدخال.ردرماز () ك نصية:
	نصية[] دونقطتان = ['ئعلن'، 'ئدا'، 'وئلا'، 'لكل'، 'طالما'، 'حاول'، 'ئستدرك']؛
	نصية[] بدونتم = ['وئلا'، 'ئستدرك']؛
	نصية[] ينتهيب = [':'، '؛'، '،'، '{'، '['، '('، 'تم'، '>']؛
	
	نصية رماز = هدا.حالة.رماز.قللم()؛
	
	ئدا ينتهيب.بعضها(دالة(عنصر): رماز.ينتهيب(عنصر)):
		ئرجع هدا.حالة.رماز؛
	تم
	
	ئدا دونقطتان.بعضها(دالة(عنصر): رماز.يبدئب(عنصر)):
		ئدا بدونتم.بعضها(دالة(عنصر): رماز.يبدئب(عنصر)):
			ئرجع هدا.حالة.رماز + ':'؛
		وئلا:
			ئرجع هدا.حالة.رماز + ':\nتم'؛
		تم
	تم
	
	ئدا ليس رماز.يتضمن('{'):
		رماز = رماز.عووض('؛'، '')؛
		ئدا رماز.ينتهيب('})'):
			ئرجع رماز.عووض('})'، '\n})؛')؛
		تم
		ئدا رماز.ينتهيب('}'):
			ئرجع رماز.عووض('}'، '\n}؛')؛
		تم
	تم
	
	ئرجع هدا.حالة.رماز + '؛'؛
تم

ئعلن مشهدئدخال.عندنقرئرسال ():
	هدا.ئطلقحدت(حدتمخصص('ئرسالرماز'، {
		يتصاعد: صحيح،
		بيا: {رماز: هدا.ردرماز()}
	}))؛
	هدا.خدحالة({
		رماز: ''،
		ئنتقال: 1
	})؛
تم

ئعلن مشهدئدخال.عندنقرئدراج ():
	عدد ع = هدا.حالة.ئنتقال + 1؛
	ئدا ع > 2:
		ع = 0؛
	تم
	هدا.خدحالة({
		ئنتقال: ع
	})؛
تم

ئعلن مشهدئدخال.عندنقردكا ():
	سياق.خدحالة({
		هلدكا: ليس سياق.حالة.هلدكا
	})؛
	هدا.خدحالة({
		هلدكا: سياق.حالة.هلدكا
	})؛
تم

ئعلن مشهدئدخال.عندضغطمفتاح (حدت ح):
	ئدا ح:
		ئدا (ح ك منوع).keyCode == 13:
			#ئدخال
			ح.ئمنعمفترض()؛
			هدا.عندنقرئرسال()؛
		تم
	تم
تم

ئعلن مشهدئدخال.عندلصق (حدت ح) ك وعد:
	ح.ئمنعمفترض()؛
	نصية منسوخ = ريتما الحافضة.ئقرئنص()؛
	ئدا منسوخ.يتضمن('\n'):
		غمازة غم = المستند.ئستعلم((<غمازة>))؛
		غم.خدحالة({
			رسالة: "نص من عدة سطرين، ئلصقه ضمن الرماز"
		})؛
	وئلا:
		عنصرمربعنص مربنص = هدا.ئستعلم((<مربعنص>))؛
		عدد بد = مربنص.بدتحديد؛
		عدد نه = مربنص.نهتحديد؛
		مربنص.قيمة = مربنص.قيمة.ردقطعة(0، بد) + منسوخ + مربنص.قيمة.ردقطعة(نه)؛
		مربنص.بدتحديد = بد + منسوخ.طول؛
		مربنص.نهتحديد = بد + منسوخ.طول؛
	تم
	
	#نلصق السطر الئول فقط
	#نصية[] سطرين = منسوخ.قسسم('\n')؛
	#
	#مربنص.قيمة = سطرين[0]؛
تم

ئعلن مشهدئدخال.عندتركمفتاح (حدت ح):
	عنصرمربعنص مربنص = ح.هدف؛
	عدد بد = مربنص.بدتحديد؛
	عدد نه = مربنص.نهتحديد؛
	ئدا مربنص.قيمة لا= هدا.حالة.رماز:
		هدا.خدحالة({
			رماز: مربنص.قيمة
		})؛
		عدد جدي = بد؛
		
		مربنص = هدا.ئستعلم((<مربعنص>))؛
		(مربنص ك منوع).focus()؛
		
		مربنص.بدتحديد = جدي؛
		مربنص.نهتحديد = جدي؛
	تم
تم

ئعلن مشهدئدخال.حممل ():
	هدا.خدحالة({
		هلدكا: سياق.حالة.هلدكا
	})؛
تم

ئعلن مشهدئدخال.ئمرر (عنصرمربعنص محرر):
	عنصرئشتمل تضليل = هدا.ئستعلم((<منسق>))؛
	تضليل.مررئعلا = محرر.مررئعلا؛
	تضليل.مرريسار = محرر.مرريسار؛
تم

ئعلن مشهدئدخال.عندتركيزئدخال ():
	عنصرئشتمل ئستكعام = هدا.ئستعلم('#ئستكمالعام')؛
	ئستكعام.ئسمصنف = ئستكعام.ئسمصنف.عووض('بلاعرض'، 'مخطط-عرضي')؛
تم

ئعلن مشهدئدخال.عندلاتركيزئدخال ():
	عنصرئشتمل ئستكعام = هدا.ئستعلم('#ئستكمالعام')؛
	#ئستكعام.ئسمصنف = ئستكعام.ئسمصنف.عووض('مخطط-عرضي'، 'بلاعرض')؛
تم

ئعلن مشهدئدخال.حددت ():
	# نحفض الموضع الحالي لمئشر الكتابة
	عنصرمربعنص مربنص = هدا.ئستعلم((<مربعنص>))؛
	عدد بد = مربنص.بدتحديد؛
	عدد نه = مربنص.نهتحديد؛
	
	هدا.حددتمشهد()؛
	هدا.ئربطحدتين()؛
	مربنص = هدا.ئستعلم((<مربعنص>))؛
	مربنص.ئضفمستمعحدت('تركمفتاح'، دالة (حدت ح): هدا.عندتركمفتاح(ح))؛
	مربنص.ئضفمستمعحدت('ضغطمفتاح'، دالة (حدت ح): هدا.عندضغطمفتاح(ح))؛
	مربنص.ئضفمستمعحدت('لصق'، دالة (حدت ح): هدا.عندلصق(ح))؛
	مربنص.ئضفمستمعحدت('تمرير'، دالة (هدف، حدت ح): هدا.ئمرر(مربنص))؛
	مربنص.ئضفمستمعحدت('تركيز'، دالة (هدف، حدت ح): هدا.عندتركيزئدخال())؛
	مربنص.ئضفمستمعحدت('لاتركيز'، دالة (هدف، حدت ح): هدا.عندلاتركيزئدخال())؛
	#(مربنص ك منوع).focus()؛
	
	# نستعيد المئشر
	مربنص = هدا.ئستعلم((<مربعنص>))؛
	مربنص.بدتحديد = مربنص.قيمة.طول؛
	مربنص.نهتحديد = مربنص.قيمة.طول؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مشهدئدخال'، مشهدئدخال)؛
تم

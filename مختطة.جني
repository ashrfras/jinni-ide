ئورد رجكس؛
ئورد الكل ك رياضيات من رياضيات؛
ئورد مجال من مصفوفة؛
ئورد بتشمرفق من مكون.بتشدبي؛
ئورد الئان من تاريخ؛

ئعلن مركب مختطمشروع:
	نصية _معرف،
	نصية _مراجعة،
	منطق _محدوف،
	بتشمرفق[] _مرفقات،
	نصية رابط،
	نصية ئسمشروع،
	نصية توكن،
	نصية مالك،
	نصية مستودع،
	نصية وصف،
	نصية نبدة،
	عدد ئاخرفتح،
	عدد ئاخرئيداع،
	عدد ئاخرتعديل،
	نصية فرعمفترض
تم

ئعلن مركب مختطوحدة:
	نصية _معرف،
	نصية _مراجعة،
	منطق _محدوف،
	بتشمرفق[] _مرفقات،
	تعداد نوع [وحدة]،
	نصية مسار،
	عدد رتبة،
	نصية معرفمشروع،
	عدد ئاخرتعديل،
	نصية مسارنسبي، #لا تحفض في القاعدب
	مختطوحدة[] فرعيات، #لا تحفض في القاعدب
	تعداد نوعمدخلة [جني، مجلد، ملف] #لا تحفض في القاعدب
تم

ئعلن مركب مختطبطاقة:
	نصية _معرف،
	نصية _مراجعة،
	منطق _محدوف،
	بتشمرفق[] _مرفقات،
	تعداد نوع [بطاقة]،
	نصية عنوان،
	نصية وحدة،
	نصية معرفمشروع،
	نصية محتوا
تم

ئعلن ئستولدمعرف (عدد طول = 6) ك نصية:
	نصية محرفين = 'ئبجدهوزحطيكلمنصعفصقرشتخضغ0123456789'؛
	نصية نتج = ''؛
	لكل ع في مجال(0، طول):
		عدد مئشرعشوائي = رياضيات.ئستصغر(رياضيات.عشوائي() × محرفين.طول)؛
		نتج = نتج + محرفين[مئشرعشوائي]؛
	تم
	ئرجع نتج؛
تم

ئعلن عنوانمنمحتوا (نصية محتو):
	رجكس رج = رجكس('ئعلن\\s*(.+)\\s*\\(.*?\\)(.*):')؛
	نصية[] نتج = محتو.طابق(رج)؛
	ئدا نتج:
		ئرجع نتج[1].قللم()؛
	وئلا:
		ئرجع ''؛
	تم
تم


ئعلن ئنشئبطاقة (نصية مساروحدة، نصية محتوا) ك مختطبطاقة:
	ئرجع {
		_معرف: ئستولدمعرف(10)،
		نوع: 'بطاقة'،
		عنوان: عنوانمنمحتوا(محتوا)،
		وحدة: مساروحدة،
		معرفمشروع: 'مشروع'،
		محتوا: محتوا
	}؛
تم

ئعلن ئنشئوحدة (نصية مسار) ك مختطوحدة:
	ئرجع {
		_معرف: مسار،
		نوع: 'وحدة'،
		مسار: مسار،
		رتبة: مسار.قسسم('/').طول-1،
		معرفمشروع: 'مشروع'،
		ئاخرتعديل: الئان()
	}؛
تم

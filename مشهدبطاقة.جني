ئورد مكون، عنصرمخصص، عنصر، عنصرئشتمل؛
ئورد حدت، حدتمخصص؛
ئورد ئطاردوم، عنصرمربعنص؛
ئورد المستند من مستند؛
ئورد النافدة من نافدة؛
ئورد الئان من تاريخ؛
ئورد رجكس؛

ئورد مشهدئدخال؛
ئورد مشهدكود؛

ئورد طافية من مكون.طافية؛
ئورد مسترمز من مكون.مسترمز؛
ئورد نعملا من مكون.نعملا؛
ئورد غمازة من مكون.غمازة؛
ئورد مكون.منسدلة؛
ئورد الكل ك قاعدب من قاعدب؛
ئورد سياق من سياق؛
ئورد مختطبطاقة من صنفمجال.بطاقة؛
ئورد مختطوحدة من صنفمجال.وحدة؛
ئورد الكل ك مجالوحدة من صنفمجال.وحدة؛
ئورد الكل ك مجالبطاقة من صنفمجال.بطاقة؛
ئورد الكل ك مجالمشروع من صنفمجال.مشروع؛
ئورد مجال من مصفوفة؛

ئورد ترجم من مكون.مترجني؛


ئعلن مشهدبطاقة ()؛
ئعلن مشهدبطاقة.عندحفضبطاقة (منطق ئغلق؟ = صحيح) ك وعد؛
ئعلن مشهدبطاقة.ئحفضوشغغل () ك وعد؛
ئعلن مشهدبطاقة.ضغطئدخال (حدتمخصص ح) ك وعد؛
ئعلن مشهدبطاقة.ئضغطسطرجديد (حدتمخصص ح) ك وعد؛
ئعلن مشهدبطاقة.تحققوحدة (نصية مساروحدة) ك وعد منطق؛
ئعلن مشهدبطاقة.ئنسخبطاقة () ك وعد؛
ئعلن مشهدبطاقة.ئنشئوحدة (نصية مساروحدة) ك وعد؛
ئعلن مشهدبطاقة.عندغلقبطاقة ()؛
ئعلن مشهدبطاقة.ئحدفبطاقة (مختطبطاقة بطا) ك وعد؛
ئعلن مشهدبطاقة.صيير ()؛
ئعلن مشهدبطاقة.عندحدفبطاقة () ك وعد؛
ئعلن مشهدبطاقة.حممل ()؛
ئعلن مشهدبطاقة.عندنقرتراجع ()؛
ئعلن مشهدبطاقة.ئعرضخطئين ()؛
ئعلن مدخل ()؛

ئعلن مشهدبطاقة ():
	يمدد مكون()؛
	
	يملك مركب { مختطبطاقة بطاقة، منطق ضاهر } حالة = {
		بطاقة: عدم،
		ضاهر: خطئ
	}؛
	
	يملك حدت _عندغلقبطاقة؛
	يملك منطق تمتعديل = خطئ؛
تم

ئعلن مشهدبطاقة.عندتحديدسطر(ح):
	مشهدئدخال مشئد = هدا.ئستعلم((<مشهدئدخال>))؛
	مركب {نصية قيمة} بيا = ح.بيا؛
	مشئد.خدحالة({
		رماز: بيا.قيمة
	})؛
تم

ئعلن مشهدبطاقة.حددت ():
	هدا.حددتمشهد()؛
	ئدا هدا.حالة.ضاهر:
		هدا.ئربطحدتين()؛
		هدا.تمتعديل = خطئ؛
		#مسترمز مسترم = هدا.ئستعلم((<مسترمز>))؛
		مشهدكود مشكود = هدا.ئستعلم((<مشهدكود>))؛
		مشهدئدخال مشدخل = هدا.ئستعلم((<مشهدئدخال>))؛
		مختطبطاقة بطاقتي = هدا.حالة.بطاقة؛
		مشكود.خدحالة({
			مصدر: بطاقتي.محتوا ئدا بطاقتي وئلا ''
		})؛
		#مسترم.ئضفمستمعحدت('تغييرسطرين'، دالة(): هدا.ضغطئدخال())؛
		مشكود.ئضفمستمعحدت('تحديدسطر'، دالة(ح): هدا.عندتحديدسطر(ح))؛
		مشكود.ئضفمستمعحدت('تعديلمصدر'، دالة(ح): هدا.ئحفضوشغغل())؛
		مشدخل.ئضفمستمعحدت('ئرسالرماز'، دالة(حدتمخصص ح): هدا.ضغطئدخال(ح))؛
		مشدخل.ئضفمستمعحدت('سطرجديد'، دالة(حدتمخصص ح): هدا.ئضغطسطرجديد(ح))؛
		ئدا هدا.حالة.بطاقة:
			#هدا.ضغطئدخال()؛
			هدا.ئعرضخطئين()؛
		تم
	تم
تم

ئعلن مشهدبطاقة.عندحفضبطاقة (منطق ئغلق؟ = صحيح) ك وعد:
	#مسترمز مسترم = هدا.ئستعلم((<مسترمز>))؛
	#نصية مصدر = مسترم.حالة['مصدر']؛
	
	عنصرئشتمل حيزوحدة = هدا.ئستعلم('#حيزوحدة')؛
	نصية مساروحدة = حيزوحدة.نصداخلي.عووضكل(' '، '')؛ #نحدف المسافات
	
	مشهدكود مشكود = هدا.ئستعلم((<مشهدكود>))؛
	نصية مصدر = مشكود.حالة.مصدر؛

	ئدا مصدر:
		#البطاقة ليست فارغة
		ئدا ريتما هدا.تحققوحدة(مساروحدة):
			ئدا ئغلق:
				#في حال كان هاد الحفض ئغلاق، فيلزمنا تقسيم البطاقة
				مختطبطاقة[] بطاقاتي = ريتما مجالبطاقة.بطاقاتمنمحتوا(مجالمشروع.مشروعحالي._معرف، مساروحدة، مصدر، صحيح)؛
				ئدا بطاقاتي.طول > 1:
					#تم تقسيم البطاقات ئلا ئكتر من واحدة
					ئدا هدا.حالة.بطاقة:
						ريتما مجالبطاقة.ئحدف(قاعدب.قابمحلي، هدا.حالة.بطاقة، خطئ)؛
					تم
					ريتما مجالبطاقة.ئحفضعديد(قاعدب.قابمحلي، بطاقاتي، مساروحدة)؛
				وئلا:
					مختطبطاقة بطاقتي = بطاقاتي[0]؛
					#لم يتم التقسيم
					ئدا هدا.حالة.بطاقة:
						بطاقتي._معرف = هدا.حالة.بطاقة._معرف؛
						بطاقتي._مراجعة = هدا.حالة.بطاقة._مراجعة؛
						بطاقتي.نوعبطاقة = هدا.حالة.بطاقة.نوعبطاقة؛
					تم
					ريتما مجالبطاقة.ئحفض(قاعدب.قابمحلي، بطاقتي)؛
				تم
				هدا.خدحالة({
					بطاقة: عدم،
					ضاهر: خطئ
				})؛
			وئلا:
				مختطبطاقة بطاقتي = مجالبطاقة.ئنشئبطاقة(مساروحدة، مصدر)؛
				ئدا هدا.حالة.بطاقة:
					بطاقتي._معرف = هدا.حالة.بطاقة._معرف؛
					بطاقتي._مراجعة = هدا.حالة.بطاقة._مراجعة؛
					بطاقتي.نوعبطاقة = هدا.حالة.بطاقة.نوعبطاقة؛
				تم
				هدا.حالة.بطاقة = ريتما مجالبطاقة.ئحفض(قاعدب.قابمحلي، بطاقتي)؛
			تم
		تم
	تم
تم


ئعلن مشهدبطاقة.ئحفضوشغغل () ك وعد:
	هدا.تمتعديل = صحيح؛
	هدا.عندحفضبطاقة(ئغلق: خطئ)؛
	حاول:
		ريتما ترجم("%{مجالمشروع.مشروعحالي.ئسمشروع}.جني")؛
		مجالبطاقة.ئفرغخطئين()؛
	ئستدرك ف ك فشل:
		ريتما مجالبطاقة.بطاقاتمنخطئين(قاعدب.قابمحلي، ف)؛	
	تم
	هدا.ئعرضخطئين()؛
	هدا.ئطلقحدت(حدتمخصص('حفضبطاقة'، {يتصاعد: صحيح}))؛
تم


ئعلن مشهدبطاقة.ئضغطسطرجديد (حدتمخصص ح) ك وعد:
	مشهدكود مشكود = هدا.ئستعلم((<مشهدكود>))؛
	مشكود.سطرجديد()؛
	هدا.عندحفضبطاقة(ئغلق: خطئ)؛
تم


ئعلن مشهدبطاقة.ضغطئدخال (حدتمخصص ح) ك وعد:
	نصية سطر = ح.بيا.رماز؛
	مشهدكود مشكود = هدا.ئستعلم((<مشهدكود>))؛
	مشهدئدخال مشدخل = هدا.ئستعلم((<مشهدئدخال>))؛
	مشكود.عددلسطر(ح.بيا.رماز، مشدخل.حالة.ئنتقال)؛
	هدا.ئحفضوشغغل()؛
تم

ئعلن مشهدبطاقة.ئعرضخطئين ():
	عنصرئشتمل ئيقوخ = هدا.ئستعلم('.ئيقوخطئين')؛
	عنصرئشتمل حيزخ = هدا.ئستعلم('#رسالتخطئ')؛
	عنصرئشتمل حيز = هدا.ئستعلم('#محددخطئ')؛
	مشهدكود مشكود = هدا.ئستعلم((<مشهدكود>))؛
	#عنصرمربعنص تحرير = هدا.ئستعلم('.تحرير')؛
	
	منطق شاشتكبيرة = النافدة.عرضداخلي > 768؛
	
	دع خطئي = مجالبطاقة.ردمعلوخطئين(هدا.حالة.بطاقة)؛
	
	ئدا خطئي:
		نصية سطرئدخال = خطئي['سطرئدخال']؛
		#حيزخ.نصمحتوا = خطئي['رسالة']؛
		نصية[] محتوامقسم = مشكود.حالة.مصدر.قسسم('\n')؛
		عدد كمسطرقبل = محتوامقسم.ردموضع(سطرئدخال)؛
		ئيقوخ.ئسمصنف = ئيقوخ.ئسمصنف.عووض('مخفي'، 'ضاهر')؛
		#حيزخ.ئسمصنف = حيزخ.ئسمصنف.عووض('مخفي'، 'ضاهر')؛
		
		ئدا كمسطرقبل >= 0:
			عدد طولسطر = 22؛
			عدد طولرئسية = 57؛
			ئطاردوم ئطار = مشكود.ردئطارمشهد()؛
			ئطاردوم ئطارحيز = حيز.ردئطارمشهد()؛
			
			حيز.ئسمصنف = حيز.ئسمصنف.عووض('مخفي'، 'ضاهر')؛
			حيز.نصمحتوا = سطرئدخال؛
			
			ئدا شاشتكبيرة:
				حيز.سمة['top'] = "%{(ئطار.فوق + (كمسطرقبل × طولسطر)) - طولرئسية}px"؛
			وئلا:
				حيز.سمة['top'] = "%{ئطار.فوق + (كمسطرقبل × طولسطر)}px"؛
			تم
			حيز.سمة['right'] = "1rem"؛				
			حيز.سمة['width'] = "%{ئطار.عرض}px"؛
			حيز.سمة['height'] = "%{طولسطر}px"؛
			حيز.سمة['z-index'] = '100000'؛
			حيز.سمة['opacity'] = '0.2'؛
			حيز.سمة['pointerEvents'] = 'none'؛
				
			حيز.نصمحتوا = ''؛		
		تم
	وئلا:
		#ئيقوخ.ئسمصنف = ئيقوخ.ئسمصنف.عووض('ضاهر'، 'مخفي')؛
		#حيزخ.نصمحتوا = ''؛
		#حيزخ.ئسمصنف = حيزخ.ئسمصنف.عووض('ضاهر'، 'مخفي')؛
		حيز.ئسمصنف = حيز.ئسمصنف.عووض('ضاهر'، 'مخفي')؛
	تم
تم

ئعلن مشهدبطاقة.تحققوحدة (نصية مساروحدة) ك وعد منطق:
	نعملا نعلا = هدا.ئستعلم((<نعملا>))؛
	
	نصية مساروح = مساروحدة؛
	مركب {منطق هلنجح، نصية نصخطئ، عدد رمزخطئ} نتج = ريتما مجالوحدة.تحققوحدة(قاعدب.قابمحلي، مساروحدة)؛
	
	ئدا ليس نتج.هلنجح:
		ئدا نتج.رمزخطئ == 404:
			نعلا.خدحالة({
				رسالة: "%{نتج.نصخطئ}. هل ترغب في ئنشائها؟"،
				زرلا: 'ئرجع'،
				زرنعم: 'نعم'،
				عندنعم: (دالة(): هدا.ئنشئوحدة(مساروح))،
				ئيقو: 'تحدير-دائرة'
			})؛
		وئلا:
			نعلا.خدحالة({
				رسالة: نتج.نصخطئ،
				زرلا: 'ئرجع'،
				ئيقو: 'تحدير-دائرة'
			})؛
		تم	
		ئرجع خطئ؛
	وئلا:
		ئرجع صحيح؛
	تم
تم
ئعلن مشهدبطاقة.ئنسخبطاقة () ك وعد:
	مسترمز مسترم = هدا.ئستعلم((<مشهدكود>))؛
	غمازة غم = المستند.ئستعلم((<غمازة>))؛
	عنصرئشتمل حيزوحدة = هدا.ئستعلم('#حيزوحدة')؛
	نصية مساروحدة = حيزوحدة.نصداخلي.عووضكل(' '، '')؛ #نحدف المسافات
	نصية مصدر = مسترم.حالة.مصدر؛
	
	ئدا ريتما هدا.تحققوحدة(مساروحدة):
		مختطبطاقة بطاقتي = مجالبطاقة.ئنشئبطاقة(مساروحدة، مصدر)؛
		ريتما مجالبطاقة.ئحفض(قاعدب.قابمحلي، بطاقتي)؛
		غم.خدحالة({
			رسالة: 'تم ئستنساخ البطاقة'
		})؛
	تم
تم

ئعلن مشهدبطاقة.ئنشئوحدة (نصية مساروحدة) ك وعد:
	غمازة غم = المستند.ئستعلم((<غمازة>))؛
	مختطوحدة وح؛
	حاول:
		وح = مجالوحدة.ئنشئوحدة(مسار: مساروحدة، هليدقق: صحيح)؛
	ئستدرك ف ك فشل:
		غم.خدحالة({
			رسالة: ف.رسالة
		})؛
		ئرجع خطئ؛
	تم
	ريتما مجالوحدة.ئحفض(قاعدب.قابمحلي، وح)؛
	هدا.ئطلقحدت(حدتمخصص('ئنشائوحدة'، {يتصاعد: صحيح، بيا: مساروحدة}))؛
	غم.خدحالة({
		رسالة: 'تم ئنشائ الوحدة'
	})؛
	ئرجع صحيح؛
تم


ئعلن مشهدبطاقة.عندغلقبطاقة ():
	هدا.ئطلقحدت(حدتمخصص('ئغلاقبطاقة'، {يتصاعد: صحيح}))؛
	ئدا هدا.تمتعديل:
		هدا.عندحفضبطاقة(ئغلق: صحيح)؛
	وئلا:
		هدا.خدحالة({
			بطاقة: عدم،
			ضاهر: خطئ
		})؛
	تم
تم


ئعلن مشهدبطاقة.ئحدفبطاقة (مختطبطاقة بطا) ك وعد:
	غمازة غم = المستند.ئستعلم((<غمازة>))؛
	مركب {منطق حدفبطاقة، منطق حدفوحدة} نتج = ريتما مجالبطاقة.ئحدف(قاعدب.قابمحلي، بطا)؛
	
	ئدا نتج.حدفوحدة:
		هدا.ئطلقحدت(حدتمخصص('حدفوحدة'، {يتصاعد: صحيح}))؛
	وئلا:
		هدا.ئطلقحدت(حدتمخصص('حدفبطاقة'، {يتصاعد: صحيح}))؛
	تم

	غم.خدحالة({
		رسالة: 'تم حدف البطاقة'
	})؛
	
	هدا.عندغلقبطاقة()؛
تم

ئعلن مشهدبطاقة.صيير ():
	منطق شاشتكبيرة = النافدة.عرضداخلي > 768؛
	نصية طول = '100dvh'؛
	ئدا شاشتكبيرة:
		طول = 'calc(100dvh - 60px)'؛ #60 طول الترويس
	تم
	مختطبطاقة بطاقة = هدا.حالة.بطاقة؛
	نصية مساروحدة = ' '؛
	ئدا هدا.حالة.بطاقة:
		مساروحدة = هدا.حالة.بطاقة.وحدة؛
	وئلا:
		مساروحدة = سياق.حالة.مساروحدتحالية.عووض('/'، '')؛
	تم
	ئدا مساروحدة == '':
		#نضيف مسافة للوحدة الفارغة لتمكين تعديلها
		مساروحدة = ' '؛
	تم
	نصية عنوان = 'بطاقة جديدة'؛
	ئدا هدا.حالة.بطاقة:
		ئدا هدا.حالة.بطاقة.عنوان لا= '':
			عنوان = هدا.حالة.بطاقة.عنوان؛
			ئدا هدا.حالة.بطاقة.نوعبطاقة == 'وضيفة':
				عنوان = "%{عنوان}()"؛
			تم
		وئلا ئدا هدا.حالة.بطاقة.محتوا لا= '':
			عنوان = ''؛
		تم
	تم
	
	ئدا (['ئوردين'، 'متغيرين'، 'ترويسة']).يتضمن(عنوان):
		عنوان = ''؛
	تم
	
	ئرجع (
		<شرط>
			<عند تحقق="هدا.حالة.ضاهر">
				<قسم صنف="ط-كامل ع-كامل خلف-حيادي-800">
					<قسم صنف="مخطط-طولي فجوة-3 حش-4" سمة="box-sizing:border-box;height:%{طول}">
						<قسم صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط فجوة-3 نص-ئبيض">
							<ئيقو ئسم="سهم-تصفح-يمين" صنف="مئشر-سبابة نصكجج" عندنقر="عندغلقبطاقة"></ئيقو>
							<قسم صنف="مخطط-عرضي فجوة-1 يكبر ئصفف-وسط">
								<حيز صنف="نصصغ نص-ئبيض خلف-حيادي-800 ئطاربلا">%{عنوان}</حيز>
								<ئيقو ئسم="تحدير-دائرة-ملئ" صنف="ئيقوخطئين مخفي ئحمر-700 نفسه-بداية"></ئيقو>
							</قسم>
							<منسدلة صنف="داكن">
								<ئيقو ئسم="تلاتنقط-عمودي" صنف="مئشر-سبابة نصكجج"></ئيقو>
								<ئطارمنسدلة صنف="نهاية-2 فوق-2">
									<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-وسط">
										<ئيقو ئسم="تكرار" صنف="نصكب"></ئيقو>
										<حيز صنف="نصصج">تراجع</حيز>
									</قسم>
									<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-وسط">
										<ئيقو ئسم="تراجع" صنف="نصكب"></ئيقو>
										<حيز صنف="نصصج">ئعادة</حيز>
									</قسم>
									<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-وسط" عندنقر="عندحدفبطاقة">
										<ئيقو ئسم="قمامة" صنف="نصكب"></ئيقو>
										<حيز صنف="نصصج">حدف البطاقة</حيز>
									</قسم>
									<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-وسط" عندنقر="ئنسخبطاقة">
										<ئيقو ئسم="نسخ" صنف="نصكب"></ئيقو>
										<حيز صنف="نصصج">تكرار البطاقة</حيز>
									</قسم>
								</ئطارمنسدلة>
							</منسدلة>
						</قسم>
						
						<قسم صنف="يكبر فلكس فائض-مخفي يصغر">
							<مشهدكود صنف="ع-كامل"></مشهدكود>
						</قسم>
						
						<قسم صنف="مخطط-عرضي">
							<قسم صنف="مخطط-عرضي ئصفف-وسط ئرصف-وسط فجوة-3 نص-ئبيض ئطار-رمادي-700 حشص-1 حشس-2 مقوس ئطار1">
								<ئيقو ئسم="مربع-قوس-كود"></ئيقو>
								<قسم صنف="مخطط-عرضي نصصج" سمة="line-height:1.3rem;">
									<حيز>/</حيز>
									<حيز معرف="حيزوحدة" قابلتعديل="plaintext-only">%{مساروحدة}</حيز>
								</قسم>
							</قسم>
						</قسم>
						
						<مشهدئدخال></مشهدئدخال>
						
						
					</قسم>
					
					<حيز معرف="محددخطئ" صنف="موضعمطلق مخفي خلف-ئحمر-700"></حيز>	
					<حيز معرف="رسالتخطئ" صنف="مخفي ع-تسعون خلف-ئحمر-700 حش-2 نص-ئبيض نصصجج موضعمطلق تحت-16 مقوس"></حيز>
					
					<نعملا صنف="داكن" نبرة="ئحمر-700" عنصر-عنوان="نصصغ" عنصر-رسالة="نصصج ئرتف-سطر-واسع:هام" عنصر-زرنعم="نصصج" عنصر-زرلا="نصصج"></نعملا>
				</قسم>
			</عند>
		</شرط>
	)؛
تم

ئعلن مشهدبطاقة.عندنقرخطئ ():
	عنصرئشتمل حيز = هدا.ئستعلم('#رسالتخطئ')؛
	ئدا حيز.ئسمصنف.يتضمن('مخفي'):
		حيز.ئسمصنف = حيز.ئسمصنف.عووض('مخفي'، 'ضاهر')؛
	وئلا:
		حيز.ئسمصنف = حيز.ئسمصنف.عووض('ضاهر'، 'مخفي')؛
	تم
تم

ئعلن مشهدبطاقة.عندحدفبطاقة () ك وعد:
	مختطبطاقة بطاقتي = هدا.حالة.بطاقة؛
	نعملا نعلا = هدا.ئستعلم((<نعملا>))؛
	نعلا.خدحالة({
		رسالة: 'هل متئكد من رغبتك في حدف هده البطاقة؟'،
		عندنعم: (دالة(): هدا.ئحدفبطاقة(بطاقتي))،
		ئيقو: 'خطر-دائرة'
	})؛
تم

ئعلن مشهدبطاقة.عندنقرنضف ():
	مسترمز مسترم = هدا.ئستعلم((<مسترمز>))؛
	مسترم.نضضف()؛
تم

ئعلن مشهدبطاقة.عندنقرتراجع ():
	مسترمز مسترم = هدا.ئستعلم((<مسترمز>))؛
	مسترم.تراجع()؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مشهدبطاقة'، مشهدبطاقة)؛
تم
ئورد مكون، عنصرمخصص، حدت، وضيفة؛
ئورد المستند من مستند؛
ئورد زر من مكون.زر؛

ئعلن نعملا ():
	يمدد مكون()؛
	
	يملك مركب {
		نصية ئيقو،
		نصية عنوان،
		نصية رسالة،
		نصية زرنعم،
		نصية زرلا،
		وضيفة عندنعم،
		وضيفة عندلا
	} حالة = {
		ئيقو: عدم،
		عنوان: ''،
		رسالة: ''،
		زرنعم: 'نعم'،
		زرلا: 'لا'،
		عندنعم: عدم،
		عندلا: عدم
	}؛
	
	يملك تعداد موضع [فوق، وسط، تحت]؛
	يملك نصية نبرة؛
	يملك نصية صنفئيقو؛
	يملك نصية صنفعنوان؛
	يملك نصية صنفرسالة؛
	يملك نصية صنفزرلا؛
	يملك نصية صنفزرنعم؛
	يملك نصية صنفبطاقة؛
	
	هدا.نبرة = هدا.ردخاصية('نبرة') ئو 'ئسود'؛
	
	هدا.صنفئيقو = هدا.ردخاصية('عنصر-ئيقو') ئو ''؛
	هدا.صنفعنوان = هدا.ردخاصية('عنصر-عنوان') ئو ''؛
	هدا.صنفرسالة = هدا.ردخاصية('عنصر-رسالة') ئو ''؛
	هدا.صنفزرلا = هدا.ردخاصية('عنصر-زرلا') ئو ''؛
	هدا.صنفزرنعم = هدا.ردخاصية('عنصر-زرنعم') ئو ''؛
	هدا.صنفبطاقة = هدا.ردخاصية('عنصر-بطاقة') ئو ''؛
	
	قل هدا.صنفرسالة؛
تم

ئعلن نعملا.صيير ():	
	ئرجع (
		<شرط>
			<عند تحقق="هدا.حالة.رسالة لا= ''">
				<قسم صنف="مخطط موضعتابت فوق-0 يمين-0 ط-شاشة ع-شاشة خلف-ئسود شفافية50 محورض2000">
				</قسم>
				<قسم صنف="مخطط ئرصف-وسط ئصفف-وسط نعملاستار موضعتابت فوق-0 يمين-0 ط-شاشة ع-شاشة محورض2000" عندنقر="ئخفينعملا">
					
					<قسم صنف="مخطط-طولي ئصفف-وسط ئرصف-وسط حش-6 خلف-ئبيض ئسود داكن:خلف-حيادي-800 داكن:ئبيض ع-164 %{هدا.صنفبطاقة}">
						<قسم صنف="مخطط-طولي فجوة-4 ئصفف-وسط ئرصف-وسط حشص-6">
							<شرط>
								<عند تحقق="هدا.حالة.ئيقو">
									<ئيقو صنف="%{هدا.صنفئيقو} نصكج3" ئسم="%{هدا.حالة.ئيقو}"></ئيقو>
								</عند>
							</شرط>
							<قسم صنف="مخطط-طولي ئصفف-وسط ئرصف-وسط فجوة-1">
								<حيز صنف="نصكج3 خطسميك نصوسط %{هدا.صنفعنوان}">%{هدا.حالة.عنوان}</حيز>
								<حيز صنف="%{هدا.صنفرسالة}">%{هدا.حالة.رسالة}</حيز>
							</قسم>
							<قسم صنف="مخطط-عرضي فجوة-2 ئصفف-وسط ئرصف-وسط">
								<شرط>
									<عند تحقق="هدا.حالة.زرلا لا= عدم">
										<زر عادي تانوي قيمة="%{هدا.حالة.زرلا}" صنف="نعملازرلا %{هدا.صنفزرلا}" نبرة="%{هدا.نبرة}" عندنقر="نقرلا"></زر>
									</عند>
								</شرط>
								<شرط>
									<عند تحقق="هدا.حالة.زرنعم لا= عدم">
										<زر عادي قيمة="%{هدا.حالة.زرنعم}" صنف="نعملازرنعم %{هدا.صنفزرنعم}" نبرة="%{هدا.نبرة}" عندنقر="نقرنعم"></زر>
									</عند>
								</شرط>
							</قسم>			
						</قسم>
					</قسم>
					
				</قسم>
			</عند>
		</شرط>
	)؛
تم

ئعلن نعملا.حددت ():
	نعملا نفسه = هدا؛
	هدا.حددتمشهد()؛
	هدا.ئربطحدتين()؛
	مكون زرنعم = هدا.ئستعلم('.نعملازرنعم')؛
	مكون زرلا = هدا.ئستعلم('.نعملازرلا')؛
	ئدا زرنعم:
		نفسه = هدا؛
	تم
	ئدا زرلا:
		نفسه = هدا؛
	تم
تم

ئعلن نعملا.ئخفينعملا (منوع هدف، حدت ح؟):
	نعملا نفسه = المستند.ئستعلم((<نعملا>))؛
	ئدا (ليس ح) ئو (ح.ردهدف() == المستند.ئستعلم('.نعملاستار')):
		نفسه.خدحالة({
			ئيقو: عدم،
			عنوان: ''،
			رسالة: ''،
			زرنعم: 'نعم'،
			زرلا: 'لا'
		})؛
	تم
تم

ئعلن نعملا.نقرنعم (منوع هدف، حدت ح):
	ئدا هدا.حالة.عندنعم:
		وضيفة عندنعم = هدا.حالة.عندنعم ك وضيفة؛
		عندنعم.ئستدعي(هدف)؛
	تم
	هدا.ئخفينعملا(هدف)؛
تم

ئعلن نعملا.نقرلا (منوع هدف، حدت ح):
	ئدا هدا.حالة.عندلا:
		(هدا.حالة.عندلا ك وضيفة).ئستدعي(هدف)؛
	تم
	هدا.ئخفينعملا(هدف)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('نعملا'، نعملا)؛
تم
